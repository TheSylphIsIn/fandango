const DPAD_RIGHT=0x0010
const DPAD_LEFT=0x0020
const DPAD_UP=0x0040
const DPAD_DOWN=0x0080
const A_BUTTON=0x0001
const B_BUTTON=0x0002
const FLAG_AT_BOTTOM_OF_BENCH=FLAG_TEMP_F
const LOCALID_KEESIGHT = 9
const LOCALID_ESPERSHARD = 10
const LOCALID_UMBRASHARD = 11
const LOCALID_PYTHON = 2
const LOCALID_WEATHERBANE = 1
const LOCALID_BRADLEY = 4

mapscripts EltheCity_MapScripts 
{
	MAP_SCRIPT_ON_LOAD
	{
		setrespawn(HEAL_LOCATION_ELTHE_CITY)
		if(flag(FLAG_STARTED_WEATHERBANE_QUEST))
		{
			setmetatile(5, 31, 0x387, FALSE)
			setmetatile(5, 30, 0x37F, FALSE)
			setmetatile(5, 29, 0x377, FALSE)
			setmetatile(5, 28, 0x36F, FALSE)
		}
	}
	MAP_SCRIPT_ON_TRANSITION {
		if(flag(FLAG_SCALE_DESCENDING_DONE))
		{
			setobjectxyperm(LOCALID_UMBRASHARD, 11, 21)
		}
		if(flag(FLAG_SCALE_ASCENDING_DONE))
		{
			setobjectxyperm(LOCALID_ESPERSHARD, 13, 21)
		}
		
		if(var(VAR_MAIN_GAME_STATE) == 4) // changes demi-vee's hint when you get to town
		{
			setvar(VAR_MAIN_GAME_STATE, 5)
		}
		
		if(!flag(FLAG_GOT_PYTHON))
		{
			random(4)
			copyvar(VAR_TEMP_F, VAR_RESULT)
			switch(var(VAR_TEMP_F))
			{
				case 0:
					setobjectxyperm(LOCALID_PYTHON, 34, 7)
					setobjectmovementtype(LOCALID_PYTHON, MOVEMENT_TYPE_FACE_LEFT)
					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_MAN_2)
				case 1:
					setobjectxyperm(LOCALID_PYTHON, 29, 34)
					setobjectmovementtype(LOCALID_PYTHON, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_MAN_5)
				case 2:
					setobjectxyperm(LOCALID_PYTHON, 6, 26)
					setobjectmovementtype(LOCALID_PYTHON, MOVEMENT_TYPE_LOOK_AROUND)
					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(POLIWHIRL))
				case 3:
					setobjectxyperm(LOCALID_PYTHON, 28, 15)
					setobjectmovementtype(LOCALID_PYTHON, MOVEMENT_TYPE_WANDER_DOWN_AND_UP)
					setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_MAN_2)
				
				// could add more if I wanted
			}
		}
	}
}

script EltheCity_EventScript_Python
{
	msgbox("… … … … …", MSGBOX_NPC)
	special(GetLeadMonSpecies)
	if(var(VAR_RESULT) == SPECIES_NIDOKING && flag(FLAG_CAN_FIND_PYTHON))
	{
		lock
		speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL)
		msgbox("…The footsteps sound wrong.\n" "This is him.", MSGBOX_AUTOCLOSE)
		playse(SE_PIN)
		applymovement(LOCALID_PYTHON, Common_Movement_ExclamationMark)
		delay(48)
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinIn)
		waitmovement(0)
		setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(ZOROARK))
		removeobject(VAR_LAST_TALKED)
		addobject(VAR_LAST_TALKED)
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinOut)
		waitmovement(0)
		lock
		faceplayer
		speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
		msgbox("Finally found me, eh, Amelia?\n" "Took you long enough.")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("Look, I've got a Poké Ball here\n" "that says…")
		speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
		msgbox("Wait, you still remember me!?")
		speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
		msgbox("Of course! This memory charm is the\n" "work of a Psychic type Pokémon.\p"
				"I'm a Dark type, so it doesn't affect\n" "me at all!\l" "I just chose to hide from you because\l" "watching you run around was fun.", MSGBOX_AUTOCLOSE)
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinIn)
		waitmovement(0)
		setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_MAY_NORMAL)
		removeobject(VAR_LAST_TALKED)
		addobject(VAR_LAST_TALKED)
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinOut)
		waitmovement(0)
		lock
		faceplayer
		speakername("Python", PORTRAIT_AMELIA_SMUG)
		msgbox("Excuse me, have you seen my dragon?\n" "Excuse me, can I get a room?\l" "Excuse me, can you renounce your god?\l" "Excuse me, have you seen my arm?\l" "You can't miss it, it's green!")
		speakername("Python", PORTRAIT_AMELIA_WINK)
		msgbox("Keheheheh!")
		speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
		msgbox("That seems a little uncalled for.\n" "I was worried about you, you know…")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		message("Hey, wait, you said this is caused\n" "by a Psychic type Pokémon?\p"
				"Do you think if we beat it, our\n" "memories will come back?")
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinIn)
		waitmovement(0)
		setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(ZOROARK))
		removeobject(VAR_LAST_TALKED)
		addobject(VAR_LAST_TALKED)
		applymovement(LOCALID_PYTHON, EltheCity_Movement_PythonSpinOut)
		waitmovement(0)
		lock
		faceplayer
		waitmessage
		waitbuttonpress
		speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
		msgbox("Eh. Probably. It's not erasing\n" "memories, just suppressing them, so\l" "they should come back if it stops.\p"
				"It's hiding out in that shrine near\n" "the church. Let's go check it out.", MSGBOX_AUTOCLOSE)
		delay(32)
		applymovement(LOCALID_PYTHON, EnterPokeballMovement)
		waitmovement(0)
		removeobject(LOCALID_PYTHON)
		delay(16)
		removeitem(ITEM_NEST_BALL, 1)
		playfanfare(MUS_EVOLVED)
		message("You recalled Python the Zoroark!")
		waitmessage
		waitfanfare
		waitbuttonpress
		closemessage
		givemon(SPECIES_ZOROARK, 48, ITEM_SILK_SCARF, ITEM_NEST_BALL, NATURE_CALM, 0, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_NIGHT_DAZE, MOVE_AGILITY, MOVE_FLAMETHROWER, MOVE_HYPER_BEAM, FALSE, FALSE, TYPE_NORMAL)
		setpartymemberinfo(4)
		setvar(VAR_MAIN_GAME_STATE, 30)
	}
}

movement EltheCity_Movement_PythonSpinIn
{
	face_down
	delay_4
	face_left
	delay_4
	face_up
	delay_2
	face_right
	delay_2
}

movement EltheCity_Movement_PythonSpinOut
{
	face_down
	delay_2
	face_left
	delay_2
	face_up
	delay_4
	face_right
	delay_4
	face_down
	delay_4
	face_player
}

script EltheCity_Sign_InnLeft
{
	msgbox("Elysi Inn", MSGBOX_SIGN)
}

script EltheCity_Sign_InnRight
{
	speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
	msgbox("Phew, these room prices…I'm glad\n" "Pokémon Centers were free…\p" "Then again, not like I have any\n" "money anyway…", MSGBOX_SIGN)
}

script EltheCity_Trigger_JumpOnBenchLeft_TopTile
{
	clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
	setvar(VAR_0x8004, ANIM_SIT_WEST)
	callnative(DoAmeliaOverworldAnim)
	playse(SE_BIKE_HOP)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_JumpOnBenchLeft)
	waitmovement(0)
	addobjectspriteoffset(OBJ_EVENT_ID_PLAYER, 0, -6)
	do {
		waitforscriptinput
		switch(var(VAR_RESULT))
		{
			case DPAD_LEFT:
			case A_BUTTON:
			case B_BUTTON:
				resetobjectspriteoffset(OBJ_EVENT_ID_PLAYER)
				applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_GetOffBenchLeft)
				waitmovement(0)
//			case DPAD_DOWN:
//				if (!flag(FLAG_AT_BOTTOM_OF_BENCH))
//				{
//					applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_MoveDownBench)
//					waitmovement(0)
//					setflag(FLAG_AT_BOTTOM_OF_BENCH)
//				}
//			case DPAD_UP:
//				if (flag(FLAG_AT_BOTTOM_OF_BENCH))
//				{
//					applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_MoveUpBench)
//					waitmovement(0)
//					clearflag(FLAG_AT_BOTTOM_OF_BENCH)				
//				}
			
		}
	} while (var(VAR_RESULT) != DPAD_LEFT && var(VAR_RESULT) != A_BUTTON && var(VAR_RESULT) != B_BUTTON)
	setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
	release
}

script EltheCity_Trigger_JumpOnBenchLeft_BottomTile
{
	goto(EltheCity_Trigger_JumpOnBenchLeft_TopTile)
}

script EltheCity_Trigger_JumpOnBenchRight_TopTile
{
	clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
	setvar(VAR_0x8004, ANIM_SIT_EAST)
	callnative(DoAmeliaOverworldAnim)
	playse(SE_BIKE_HOP)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_JumpOnBenchRight)
	waitmovement(0)
	addobjectspriteoffset(OBJ_EVENT_ID_PLAYER, 0, -6)
	do {
		waitforscriptinput
		switch(var(VAR_RESULT))
		{
			case DPAD_RIGHT:
			case A_BUTTON:
			case B_BUTTON:
				resetobjectspriteoffset(OBJ_EVENT_ID_PLAYER)
				applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_GetOffBenchRight)
				waitmovement(0)
//			case DPAD_DOWN:
//				if (!flag(FLAG_AT_BOTTOM_OF_BENCH))
//				{
//					applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_MoveDownBench)
//					waitmovement(0)
//					setflag(FLAG_AT_BOTTOM_OF_BENCH)
//				}
//			case DPAD_UP:
//				if (flag(FLAG_AT_BOTTOM_OF_BENCH))
//				{
//					applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_MoveUpBench)
//					waitmovement(0)
//					clearflag(FLAG_AT_BOTTOM_OF_BENCH)				
//				}
			
		}
	} while (var(VAR_RESULT) != DPAD_RIGHT && var(VAR_RESULT) != A_BUTTON && var(VAR_RESULT) != B_BUTTON)
	setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
	release
}

script EltheCity_Trigger_JumpOnBenchRight_BottomTile
{
	goto(EltheCity_Trigger_JumpOnBenchRight_TopTile)
}

script EltheCity_EventScript_Keesight
{
	if (flag(FLAG_SCALE_ASCENDING_DONE) && flag(FLAG_SCALE_DESCENDING_DONE))
	{
		speakername("Keesight", PORTRAIT_KEESIGHT)
		msgbox("… … … … …\n" "Thanks for the song.", MSGBOX_NPC)
	}
	else
	{
		speakername("Batmon", PORTRAIT_KEESIGHT)
		msgbox("… … … … … … … … … …")
		speakername(NULL, 0)
		msgbox("The Pokémon is staring intently\n" "at the pond…", MSGBOX_AUTOCLOSE)
	}
}

script EltheCity_EventScript_Weatherbane
{
	if(!flag(FLAG_BEAT_WEATHERBANE))
	{
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("So this is the thing I'm supposed\n" "to fix? It's, what, squeaky?", MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceUp)
		waitmovement(0)
		delay(16)
		addobjectspriteoffset(LOCALID_WEATHERBANE, 1, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, -2, 0)
		delay(8)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
		playse(SE_PIN)
		addobjectspriteoffset(LOCALID_WEATHERBANE, 2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, -2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, 2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, -2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, 2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, -2, 0)
		delay(8)
		addobjectspriteoffset(LOCALID_WEATHERBANE, 0, 0)
		delay(16)
		applymovement(LOCALID_WEATHERBANE, EltheCity_Movement_WeatherbaneFlap)
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_SCREAM)
		msgbox("Skrraaaaaaaaaaaw! Skrraaaaaaaaaaaw!")
		waitmovement(LOCALID_WEATHERBANE)
		speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
		msgbox("…Geez. It's a Pokémon?\n" "Well, it sure is noisy…")
		applymovement(LOCALID_WEATHERBANE, EltheCity_Movement_WeatherbaneFlap)
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_SCREAM)
		msgbox("Skrraaaaaaaaaaaw! Skrraaaaaaaaaaaw!")
		waitmovement(LOCALID_WEATHERBANE)
		speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
		msgbox("Hey! Shut the f{CENSOR}uck{UNCENSOR} up!")
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_NEUTRAL)
		msgbox("… … … … …")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("Why are you yelling?")
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_NEUTRAL)
		msgbox("The rain! The rain! I'm trying to\n" "make the rain go away!\p"
				"My cries can chase away storms.\n" "But this rain just keeps coming back!\l" "Begone! Begone!")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("So it's not a natural rainstorm?\n" "Maybe a Pokémon is summoning it…")
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_SCREAM)
		msgbox("Not natural! Not natural!\n" "Can't you feel it?\p"
				"The rain washes away your darkness.\n" "Makes you a carefree person.\l" "A fake person.\l" "Must be stopped! Must be stopped!")
		speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
		msgbox("Look, I know! Something's messing\n" "with my head. It could be the same\l" "thing that's causing the rain.\p"
				"I'm working on it. But my job right\n" "now is to get you to quiet down.")
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_SCREAM)
		msgbox("Can't shut up! Can't shut up!\p" "Begone! Begone!")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("What if I promise to clear up this\n" "rain? Will that shut you up?")
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_NEUTRAL)
		applymovement(LOCALID_WEATHERBANE, EltheCity_Movement_WeatherbaneFlap)
		msgbox("Maybe. Maybe.\p" "Show me you're strong enough!\n" "I think you're weak! Weak!")
		waitmovement(LOCALID_WEATHERBANE)
		closemessage
		createmon(1, 0, SPECIES_WEATHERBANE, 40, ITEM_NONE, 0, NATURE_CALM, 0, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_STEEL_WING, MOVE_AIR_CUTTER, MOVE_SCREECH, MOVE_ROOST, FALSE, 0, TYPE_WATER)
		dowildbattle
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_NEUTRAL)
		msgbox("Okay! Okay! You're good! You're good!\p" "I trust you. You will stop the rain.\n" "Amelia, Sun-Bringer! Hail! Hail!")
		speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
		msgbox("Please don't call me that ever again.", MSGBOX_AUTOCLOSE)
		delay(60)
		setflag(FLAG_BEAT_WEATHERBANE)
		playfanfare(MUS_CAUGHT)
		message("Finished the Innkeeper's request!")
		waitmessage
		waitfanfare
		waitbuttonpress
		closemessage
	}
	else
	{
		speakername("Weatherbane", PORTRAIT_WEATHERBANE_NEUTRAL)
		msgbox("I used to love the sun.\n" "Fire types feared me.\p"
				"But now I'm too old! Too old!\n" "Can't take the heat like I used to.\p"
				"But I'd still like to see the sun\n" "again. Too wet! Too wet!", MSGBOX_AUTOCLOSE)
	}
}

movement EltheCity_Movement_WeatherbaneFlap
{
	face_up
	delay_8
	face_left
	delay_8
	face_down
	delay_8
	face_right
	delay_8
}

script EltheCity_EventScript_BarrelGuy
{
	speakername("Stephen")
	msgbox("I tried to challenge that Nidoking…\p" "But he just took one look at my\n" "Pokémon, picked me up, and stuffed me\l" "into this barrel…\p"
			"Maybe Pokémon training isn't for me.", MSGBOX_NPC)
}

script EltheCity_BakeryWindow
{
	msgbox("The food on display looks delicious.\n" "Too bad you don't have spare money.", MSGBOX_SIGN)
}

script EltheCity_CitySign
{
	msgbox("Elthe City\n" "“The forgotten highway of life.”", MSGBOX_SIGN)
}

script EltheCity_Girlfriend
{
	speakername("Sarah")
	msgbox("When was the last time it wasn't\n" "raining, huh? And the place doesn't\l" "even have outside seating anyway!", MSGBOX_SIGN)
}

script EltheCity_Boyfriend
{
	speakername("Nathan")
	msgbox("I forgot to check the weather, okay?\n" "How was I supposed to know…", MSGBOX_SIGN)
}

script EltheCity_OldGuyBarrel
{
	speakername("Boris")
	msgbox("Something about this rainwater just\n" "doesn't seem right to me.\p"
			"It's not good to drink.", MSGBOX_NPC)
}

script EltheCity_Slowbro
{
	lock
	speakername("Slowbro")
	msgbox("Some people are almost as forgetful\n" "as me these days.\p"
			"Just go with the flow, I say.\n" "Isn't it fun?")
	speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
	msgbox("No, I hate knowing that I forgot\n" "something and not being able to figure\l" "out what it was. If I forget my old\l" "departed friends, I might lose it.")
	faceplayer
	speakername("Slowbro")
	msgbox("…Huh? Who are you?")
	speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
	msgbox("Oh, forget it.\p" "Urk. Phrasing.", MSGBOX_AUTOCLOSE)
	release
}

script EltheCity_Lombre
{
	speakername("Lilypad Kappa")
	msgbox("I don't know what everyone's\n" "complaining about. I hope this rain\l" "never ends!", MSGBOX_NPC)
}

script EltheCity_Hiker
{
	speakername("Nob")
	if(!flag(FLAG_GOT_BRADLEY))
	{
		msgbox("I thought my Magcargo could block\n" "that Nidoking's Poison attacks, but\l" "he knows Earthquake too…\l" "Where did he learn that?", MSGBOX_NPC)
	}
	else
	{
		lock
		faceplayer
		msgbox("So you taught him those moves?\n" "Pretty impressive.")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("I can't take all the credit. He was\n" "trained by a gym leader before I\l" "found him.")
		speakername("Nob")
		msgbox("Trained by a what?")
		release
	}
}

script EltheCity_Gentleman
{
	speakername("Regis")
	if(!flag(FLAG_GOT_BRADLEY))
	{
		msgbox("My poor Gothitelle didn't stand a\n" "chance against that Nidoking.\p" "I should've known that horn wasn't\n" "just for show.", MSGBOX_NPC)
	}
	else
	{
		msgbox("That was some fine work under pressure.\n" "The world needs more Rangers like you.", MSGBOX_NPC)
	}
}

script EltheCity_Lass
{
	speakername("Aurora")
	if(!flag(FLAG_GOT_BRADLEY))
	{
		msgbox("I thought I could win with my\n" "Harminnote's physical defense, but\l" "he pulled out a tricky Sludge Bomb…\l" "What a cheap shot!", MSGBOX_NPC)
	}
	else
	{
		msgbox("Grandpa says it was a waste to use\n" "all my Poké Balls to catch Minnote,\l" "but it's pretty strong now that\l" "it's evolved.", MSGBOX_NPC)
	}
}

script EltheCity_Mom
{
	speakername("Andrea")
	msgbox("I thought I would try to distract\n" "Sarah with some battles, but it\l" "looks like the arena is being held.\l" "That Nidoking must be at least as\l" "strong as a Dragonite.", MSGBOX_SIGN)
}

script EltheCity_ArenaSign
{
	msgbox("Battle Park\n" "“King of the hill”-style allowed!", MSGBOX_SIGN)
}

script EltheCity_BradleyScript
{
	lock
	faceplayer
	speakername("Bradley", PORTRAIT_NIDOKING_GRIN, PORTRAIT_DEFAULT_LEFT)
	msgbox("Grah hah hah! Another puny trainer\n" "has come to challenge me, eh?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Bradley, it's me, Amelia.\n" "What are you doing out here?")
	speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("I want to fight!\p" "You've been holding me back for years!\n" "It's time to let loose!")
	speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
	msgbox("Oh, you've forgotten about our talks,\n" "haven't you…\p"
			"Bradley, learning to control your\n" "bloodlust was YOUR idea. And it's been\l" "great for you! Chillax.")
	speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	playmoncry(SPECIES_NIDOKING, CRY_MODE_NORMAL)
	msgbox("I don't care! I want to fight.\n" "I'm enjoying this too much.")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Fine, we'll have a little spar, and\n" "then we get going, okay?")
	speakername("Bradley", PORTRAIT_NIDOKING_GRIN, PORTRAIT_DEFAULT_LEFT)
	msgbox("I don't want to fight your Pokémon.\n" "I've beaten them before already.\l" "I want to fight YOU, Amelia. You\l" "hold back too much.")
	speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
	msgbox("Well, too bad. I can't do that in\n" "public. I still have work to do here,\l" "I can't go getting kicked out.")
	speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Hrmph! Fine, then. I'll fight your\n" "team, if you're really serious.", MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO)
	{
		speakername("Bradley", PORTRAIT_NIDOKING_GRIN, PORTRAIT_DEFAULT_LEFT)
		msgbox("Come back when you're ready to give\n" "me a real challenge!")
	}
	else
	{
		speakername("Amelia", PORTRAIT_AMELIA_SMUG)
		msgbox("With pleasure! I have a new Pokémon.\n" "You're going to love him.")
		speakername("Bradley", PORTRAIT_NIDOKING_GRIN, PORTRAIT_DEFAULT_LEFT)
		msgbox("Oh? I'm intrigued. Maybe this will\n" "be worth my time after all.", MSGBOX_AUTOCLOSE)
		clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
		applymovement(LOCALID_BRADLEY, Common_Movement_WalkInPlaceDown)
		specialvar(VAR_RESULT, GetPlayerFacingDirection)
		switch(var(VAR_RESULT))
		{
			case DIR_SOUTH:
				call(EltheCity_WalkToBradleyBattle_North)
			case DIR_EAST:
				call(EltheCity_WalkToBradleyBattle_West)
			case DIR_WEST:
				call(EltheCity_WalkToBradleyBattle_East)
			case DIR_NORTH:
				call(EltheCity_WalkToBradleyBattle_South)
		}
		setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
		playmoncry(SPECIES_NIDOKING, CRY_MODE_NORMAL)
		speakername("Bradley", PORTRAIT_NIDOKING_GRIN, PORTRAIT_DEFAULT_LEFT)
		msgbox("Come at me!", MSGBOX_AUTOCLOSE)
		waitmoncry
		setflag(FLAG_WILD_AI_ON)
		createmon(1, 0, SPECIES_NIDOKING, 56, ITEM_QUICK_CLAW, ITEM_ULTRA_BALL, NATURE_LONELY, 2, MON_MALE, 0, 10, 0, 0, 10, 0, 31, 31, 31, 31, 31, 31, MOVE_POISON_JAB, MOVE_EARTHQUAKE, MOVE_MEGAHORN, MOVE_SLUDGE_BOMB, FALSE, FALSE, TYPE_GROUND)
		dowildbattle
		delay(32)
		speakername("Bradley", PORTRAIT_NIDOKING_NERVOUS, PORTRAIT_DEFAULT_LEFT)
		msgbox("Huff…I've been beaten.\p" "You were right, you know.\n" "This isn't my true calling. I'm just\l" "throwing a tantrum.")
		applymovement(LOCALID_BRADLEY, EltheCity_Movement_BradleyApproach)
		speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("Amelia, I can hardly think straight,\n" "but one thing I do know for sure…\p"
				"You've changed my life. I wasn't sure\n" "it was for the better, at first,\l" "but what I did in my youth…\l" "How that man…used me…\p"
				"I thought I couldn't recover from it,\n" "but you and your team helped me…")
		speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
		msgbox("I know, Bradley. It's alright.\n" "We all act stupid sometimes.\p"
				"You don't have to turn on the\n" "waterworks, it's already raining.")
		speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL)
		msgbox("It's a terrible day for rain.")
		speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
		msgbox("Oh! Speaking of which. I think we\n" "need to do something about this rain.\l" "Can you help me get the rest of the\l" "team back together so we can get\l" "started on that?")
		if(flag(FLAG_GOT_VICE))
		{
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Python must be around here somewhere,\n" "but I haven't seen any sign of him.")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Of course you haven't “seen” him.\n" "He must be in disguise.\p"
					"With my superior hearing, I can\n" "usually discern his illusions.\p"
					"If you let me look around, I can\n" "probably pick him out.")
			speakername("Amelia", PORTRAIT_AMELIA_WINK)
			msgbox("Okay! Sounds like a plan!", MSGBOX_AUTOCLOSE)
			delay(32)
			applymovement(LOCALID_BRADLEY, EnterPokeballMovement)
			waitmovement(0)
			removeobject(LOCALID_BRADLEY)
			delay(16)
			removeitem(ITEM_ULTRA_BALL, 1)
			playfanfare(MUS_EVOLVED)
			message("You recalled Bradley the Nidoking!")
			waitmessage
			waitfanfare
			waitbuttonpress
			closemessage
			givemon(SPECIES_NIDOKING, 56, ITEM_QUICK_CLAW, ITEM_ULTRA_BALL, NATURE_LONELY, 2, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_POISON_JAB, MOVE_EARTHQUAKE, MOVE_MEGAHORN, MOVE_SLUDGE_BOMB, FALSE, FALSE, TYPE_GROUND)
			setpartymemberinfo(3)
			setflag(FLAG_CAN_FIND_PYTHON)
			setvar(VAR_MAIN_GAME_STATE, 20)
		}
		else
		{
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Vice is hanging around in one of\n" "these buildings, I think.\l" "Let's go find her.")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Yes, ma'am.", MSGBOX_AUTOCLOSE)
			delay(32)
			applymovement(LOCALID_BRADLEY, EnterPokeballMovement)
			waitmovement(0)
			removeobject(LOCALID_BRADLEY)
			delay(16)
			removeitem(ITEM_ULTRA_BALL, 1)
			playfanfare(MUS_EVOLVED)
			message("You recalled Bradley the Nidoking!")
			waitmessage
			waitfanfare
			waitbuttonpress
			closemessage
			givemon(SPECIES_NIDOKING, 56, ITEM_QUICK_CLAW, ITEM_ULTRA_BALL, NATURE_LONELY, 2, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_POISON_JAB, MOVE_EARTHQUAKE, MOVE_ICE_BEAM, MOVE_THUNDERBOLT, FALSE, FALSE, TYPE_GROUND)
			setpartymemberinfo(3)
		}
	}
	release
}

movement EltheCity_Movement_BradleyApproach
{
	walk_slow_down * 3
}

script EltheCity_WalkToBradleyBattle_North
{
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_BradleyBattleNorth)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_BradleyBattleWest)
	waitmovement(0)
	return
}

script EltheCity_WalkToBradleyBattle_South
{
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_BradleyBattleSouth)
	waitmovement(0)
	return
}

script EltheCity_WalkToBradleyBattle_East
{
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_BradleyBattleEast)
	waitmovement(0)
	return
}

script EltheCity_WalkToBradleyBattle_West
{
	applymovement(OBJ_EVENT_ID_PLAYER, EltheCity_Movement_BradleyBattleWest)
	waitmovement(0)
	return
}

movement EltheCity_Movement_BradleyBattleNorth
{
	walk_left
	walk_down
}

movement EltheCity_Movement_BradleyBattleWest
{
	walk_down * 4
	walk_right
	face_up
	delay_16
}

movement EltheCity_Movement_BradleyBattleEast
{
	walk_down * 4
	walk_left
	face_up
	delay_16
}

movement EltheCity_Movement_BradleyBattleSouth
{
	walk_down * 3
	face_up
	delay_16
}

script EltheCity_Trigger_MusicStump1
{
	playse(SE_NOTE_C)
	turnobject(LOCALID_KEESIGHT, DIR_WEST)
	
	// descending scale
	if(var(VAR_TEMP_2) == 7 && !flag(FLAG_SCALE_DESCENDING_DONE))
	{
		delay(60)
		applymovement(LOCALID_KEESIGHT, EltheCity_Movement_KeesightHops)
		playse(SE_BIKE_HOP)
		delay(16)
		playse(SE_BIKE_HOP)
		delay(32)
		playmoncry(SPECIES_WOOBAT, CRY_MODE_NORMAL)
		waitmoncry
		delay(30)
		applymovement(LOCALID_KEESIGHT, Common_Movement_FaceUp)
		waitmovement(0)
		delay(16)
		playse(SE_M_SUPERSONIC)
		applymovement(LOCALID_UMBRASHARD, EltheCity_Movement_MoveUmbrashard)
		waitmovement(0)
		applymovement(LOCALID_KEESIGHT, Common_Movement_FaceDown)
		waitmovement(0)
		setobjectxyperm(LOCALID_UMBRASHARD, 11, 21)
		setflag(FLAG_SCALE_DESCENDING_DONE)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
		setvar(VAR_TEMP_1, 1)
	}
	
	release
}

script EltheCity_Trigger_MusicStump2
{
	playse(SE_NOTE_D)
	turnobject(LOCALID_KEESIGHT, DIR_WEST)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 1) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 6)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump3
{
	playse(SE_NOTE_E)
	turnobject(LOCALID_KEESIGHT, DIR_WEST)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 2) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 5)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump4
{
	playse(SE_NOTE_F)
	turnobject(LOCALID_KEESIGHT, DIR_SOUTH)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 3) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 4)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump5
{
	playse(SE_NOTE_G)
	turnobject(LOCALID_KEESIGHT, DIR_SOUTH)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 4) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 3)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump6
{
	playse(SE_NOTE_A)
	turnobject(LOCALID_KEESIGHT, DIR_EAST)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 5) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 2)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump7
{
	playse(SE_NOTE_B)
	turnobject(LOCALID_KEESIGHT, DIR_EAST)
	
	// ascending scale
	if(var(VAR_TEMP_1) == 6) 
	{
		addvar(VAR_TEMP_1, 1)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
	}
	
	// descending scale
	if(var(VAR_TEMP_2) == 1)
	{
		addvar(VAR_TEMP_2, 1)
	}
	else
	{
		setvar(VAR_TEMP_2, 0)
	}
	
	release
}

script EltheCity_Trigger_MusicStump8
{
	playse(SE_NOTE_C_HIGH)
	turnobject(LOCALID_KEESIGHT, DIR_EAST)
	
	
	// ascending scale
	if(var(VAR_TEMP_1) == 7 && !flag(FLAG_SCALE_ASCENDING_DONE)) 
	{
		delay(60)
		applymovement(LOCALID_KEESIGHT, EltheCity_Movement_KeesightHops)
		playse(SE_BIKE_HOP)
		delay(16)
		playse(SE_BIKE_HOP)
		delay(32)
		playmoncry(SPECIES_WOOBAT, CRY_MODE_NORMAL)
		waitmoncry
		delay(30)
		applymovement(LOCALID_KEESIGHT, Common_Movement_FaceUp)
		waitmovement(0)
		delay(16)
		playse(SE_M_SUPERSONIC)
		applymovement(LOCALID_ESPERSHARD, EltheCity_Movement_MoveEspershard)
		waitmovement(0)
		applymovement(LOCALID_KEESIGHT, Common_Movement_FaceDown)
		waitmovement(0)
		setobjectxyperm(LOCALID_ESPERSHARD, 13, 21)
		setflag(FLAG_SCALE_ASCENDING_DONE)
	}
	else
	{
		setvar(VAR_TEMP_1, 0)
		setvar(VAR_TEMP_2, 1) // can't instantly start the next scale after the cutscene from finishing the previous
	}
	
	release
}

movement EltheCity_Movement_KeesightHops
{
	jump_in_place_down * 2
}

movement EltheCity_Movement_MoveUmbrashard
{
	levitate
	delay_16
	walk_up * 3
	walk_right * 8
	walk_up
	delay_16
	stop_levitate
}

movement EltheCity_Movement_MoveEspershard
{
	levitate
	delay_16
	walk_left * 8
	walk_up
	delay_16
	stop_levitate
}

movement EltheCity_Movement_JumpOnBenchLeft
{
	lock_anim
	jump_in_place_left
}

movement EltheCity_Movement_GetOffBenchLeft
{
	unlock_anim
	walk_slow_left	
}

movement EltheCity_Movement_JumpOnBenchRight
{
	lock_anim
	jump_in_place_right
}

movement EltheCity_Movement_GetOffBenchRight
{
	unlock_anim
	walk_slow_right
}

movement EltheCity_Movement_MoveDownBench
{
	walk_slow_down
}

movement EltheCity_Movement_MoveUpBench
{
	walk_slow_up
}