const LOCALID_DEMIVEE=1
const LOCALID_AMELIA=2
const LOCALID_VINOS=3

mapscripts EltheRoad_MapScripts 
{
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAIN_GAME_STATE, 0 {
			clearflag(FLAG_NO_MUSIC_ON_WARP)
			addobjectspriteoffset(LOCALID_AMELIA, 5, 0)
			addobjectspriteoffset(LOCALID_DEMIVEE, 6, 0)
			delay(60)
			delay(60)
			delay(60)
			speakername("???")
			msgbox("Wait…is it alive…?", MSGBOX_AUTOCLOSE)
			fadescreen(FADE_FROM_WHITE)
			delay(60)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("…yes, I am. Personal space?", MSGBOX_DEFAULT)
			addobjectspriteoffset(LOCALID_DEMIVEE, 0, 0)
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Oh. Pardon me. I was considering\n" "eating you.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("Dude! Seriously?", MSGBOX_DEFAULT)
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("I saw you getting tossed around\n" "unconscious in a flood.\l" "I really didn't think you survived.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I'm made of tough stuff. It takes\n" "more than a little water to…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_AMELIA, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(LOCALID_AMELIA, Common_Movement_Delay48)
			waitmovement(0)
			removeobject(LOCALID_AMELIA)
			playse(SE_BIKE_HOP)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaJumpUp)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("The flood! What happened to my team?", MSGBOX_AUTOCLOSE)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaLookAround)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("My team…ugh, my head. I can't think.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I have their Poké Balls here.\n" "Guess I should go find them.", MSGBOX_AUTOCLOSE)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaStartLeaving)
			delay(30)
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Wait! You shouldn't go alone.", MSGBOX_AUTOCLOSE)
			playse(SE_BIKE_HOP)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeFollow)
			waitmovement(0)
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("The local Pokémon always get irate\n" "after a flood. It would be dangerous\l" "for a human wandering alone.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("I can handle myself pretty well.\n" "And I'm a Pokémon Ranger, too.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("But if you want to come along,\n" "I won't say no. Welcome aboard!", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeJoin)
			waitmovement(0)
			removeobject(LOCALID_DEMIVEE)
			playfanfare(MUS_CAUGHT)
			message("Eevee joined your party!")
			givemon(SPECIES_EEVEE, 30, ITEM_EVERSTONE, ITEM_POKE_BALL, NATURE_SERIOUS, 0, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_TAKE_DOWN, MOVE_BITE, MOVE_COPYCAT, MOVE_SWIFT, FALSE, FALSE, TYPE_DRAGON)
			waitmessage
			waitfanfare
			waitbuttonpress
			closemessage
			fadedefaultbgm
			setvar(VAR_MAIN_GAME_STATE, 1)
		}
	]
}	

script EltheRoad_EventScript_FirstTrigger
{
	lock
	delay(30)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("I meant to ask earlier, but, uh,\n" "what are you, exactly?\p" "You look like an Eevee, but those\n" "markings…are they natural?", MSGBOX_DEFAULT)
	speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Yes, I…well, I don't fully remember\n" "the circumstances. My body has gone\l" "through some changes.\p"
			"It probably has something to do\n" "with these crystals I eat…\p" "But now, I can't really evolve.\n" "I gain power from evolution stones,\l"
			"but it's different.", MSGBOX_DEFAULT)
	speakername("Amelia", PORTRAIT_AMELIA_SMUG)
	msgbox("So you're some kind of…Demi-Vee?", MSGBOX_DEFAULT)
	setpartymemberinfo(0) // Sets nickname, metloc, and Poke ball.
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
	msgbox("What?", MSGBOX_DEFAULT)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Regardless…if you find any evolution\n" "shards, I can change form with them.\p" "My stats and moves in battle\n" "depend on my current form.\p"
			"Also, I've lived here for as long\n" "as I can remember.\p" "If you need more info or advice\n" "about anything, just ask me.", MSGBOX_DEFAULT)
	speakername(NULL, 0)
	msgbox("(Press R while Demi-Vee is following\n" "you to get advice!)\p" "(He will talk about the person\n" "you're facing, or give general hints\l" "for how to progress.)", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 2)
}

script EltheRoad_EventScript_FirstSign
{
	msgbox("{CLEAR_TO 60}{LEFT_ARROW} To {CENSOR}Rehocan{UNCENSOR} Town\n" "{CLEAR_TO 76}To Elthe City {RIGHT_ARROW}", MSGBOX_SIGN)
}

script EltheRoad_EventScript_SecondSign
{
	msgbox("{CLEAR_TO 66}{LEFT_ARROW} To Elthe City\n" "{CLEAR_TO 36}BEWARE BILLIARACH WEBS {RIGHT_ARROW}", MSGBOX_SIGN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	delay(8)
	goto(EltheRoad_EventScript_SpiderRoadblock)
}

script EltheRoad_Trigger_VinosTrigger 
{
	lock
	speakername("Vine Mon")
	msgbox("Ugh…stupid flood.\n" "Washed away a perfect lurking spot.", MSGBOX_DEFAULT)
	speakername(NULL, 0)
	message("The Pokémon hasn't noticed you…\n" "What will you do?")
	waitmessage
	dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Call Out", "Step Forward", "Attack")
	closemessage
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Excuse me…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
			waitmovement(0)
			applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(48)
			speakername("Vine Mon")
			msgbox("A human…! I need to get in position!", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_VINOS, EltheRoad_Movement_VinosGetOnLog)
			waitmovement(0)
			message("Now what?")
			waitmessage
			dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Negotiate", "Step Forward", "Attack")
			closemessage
			switch(var(VAR_RESULT))
			{
				case 0:
				// Amelia tries to talk to it. It humors her. Difficult negotiation ensues
				case 1:
				// It jumps Amelia as soon as she steps forward. Do battle with +1 def/+2 speed on Vinos
				case 2:
				// She attacks and it springs forward. Do battle with +1 speed on vinos
			}
		case 1:
			applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_WalkUp)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
			waitmovement(0)
			delay(30)
			message("Now what?")
			waitmessage
			dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Call Out", "Step Forward", "Attack")
			closemessage
			switch(var(VAR_RESULT))
			{
				case 0:
				// It realizes it's on the back foot and opens up for negotiations. Easy negotiation ensues. If you talk to it from the left, you start here 
				case 1:
				// It gets startled by you being right behind it and flees.
				case 2:
				// Do battle with +1 atk on Demi-Vee
			}
		case 2:
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("Demi-Vee, attack!", MSGBOX_AUTOCLOSE)
			// setwildbattle() battle against vinos with no stat changes
			
	}
	
	// Negotiation flow
	//  - Give cheri berries: obtain 2 rock candy
	//  - Successful negotiation: obtain 2 rock candy
	//  - Passable negotiation: obtain 1 rock candy
	//  - Failed negotiation: Battle Vinos with a disadvantage.
	// - Negotiation flow: mid is +1 point. success is +2 points: fail is -1 point. You fail if you reach 0 points and pass if you reach 3.
	//  -- exhausting the questions without reaching 3 counts as a passable negotiation, as does reaching 3 points after a fail.
	//  -- some questions will be an instant failure state if you get them wrong.
	// - Question: What's your favorite type of Pokemon? (Grass: instant Fail, Dark: Mid, Fire: Success)
	// - Question: Eat one of my berries! (Accept: Fail, Question logistics: Mid, Eat trail mix: Success)
	// - Question: Why do you need to get through? (To get a bath: Fail, Cuz I don't want to fight you: Mid, For thrills: Success)
	// - Question: How long have you been training Pokemon? (30 years: mid, just started: fail, long enough: success)
	// fuck grass types. i hate em. - amelia
}

script EltheRoad_EventScript_SpiderRoadblock
{
	speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
	msgbox("Guess we can't go this way.\n" "…not that I wanted to anyway.\p" "I won't complain about avoiding\n" "a spider nest.", MSGBOX_AUTOCLOSE)
}

movement EltheRoad_Movement_AmeliaJumpUp
{
	set_visible
	jump_in_place_left
}

movement EltheRoad_Movement_AmeliaLookAround
{
	face_up
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_down
	delay_16
	face_right
	delay_16 * 3
	face_left
	delay_16 * 3
}

movement EltheRoad_Movement_AmeliaStartLeaving
{
	walk_up * 2
	walk_right * 3
	delay_16 * 2
	face_left
}

movement EltheRoad_Movement_DemiveeFollow
{
	jump_up
	walk_fast_up
	walk_fast_right * 2
}

movement EltheRoad_Movement_DemiveeJoin
{
	walk_slow_right
	enter_pokeball
}

movement EltheRoad_Movement_VinosGetOnLog
{
	walk_fast_right
	jump_right
	face_down
}
