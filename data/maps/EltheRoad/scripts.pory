const LOCALID_DEMIVEE=1
const LOCALID_AMELIA=2
const LOCALID_VINOS=3
const LOCALID_MARCEL=5
const LOCALID_LOGAN=13

mapscripts EltheRoad_MapScripts 
{
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_MAIN_GAME_STATE, 0 {
			clearflag(FLAG_NO_MUSIC_ON_WARP)
			addobjectspriteoffset(LOCALID_AMELIA, 5, 0)
			addobjectspriteoffset(LOCALID_DEMIVEE, 6, 0)
			delay(60)
			delay(60)
			delay(60)
			speakername("???")
			msgbox("Wait…is it alive…?", MSGBOX_AUTOCLOSE)
			fadescreen(FADE_FROM_WHITE)
			delay(60)
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("…yes, I am. Personal space?")
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			message("Oh, sorry. I was thinking about\n" "eating you, but I guess I won't now.")
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			addobjectspriteoffset(LOCALID_DEMIVEE, -1, 0)
			delay(5)
			waitmessage
			waitbuttonpress
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("Dude! Seriously?")
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("You got tossed around unconscious in\n" "a flood! How'd you survive, anyway?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I'm made of tough stuff. It takes\n" "more than a little water to…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_AMELIA, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(LOCALID_AMELIA, Common_Movement_Delay48)
			waitmovement(0)
			removeobject(LOCALID_AMELIA)
			playse(SE_BIKE_HOP)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaJumpUp)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("The flood! What happened to my team?", MSGBOX_AUTOCLOSE)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaLookAround)
			waitmovement(0)
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("My team…ugh, my head. I can't think.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I have their Poké Balls here.\n" "Guess I should go find them.", MSGBOX_AUTOCLOSE)
			additem(ITEM_POKE_BALL)
			additem(ITEM_GREAT_BALL)
			additem(ITEM_ULTRA_BALL)
			additem(ITEM_NEST_BALL)
			applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaStartLeaving)
			delay(30)
			speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Wait! You shouldn't go alone.", MSGBOX_AUTOCLOSE)
			playse(SE_LEDGE)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeFollow)
			waitmovement(0)
			speakername("Eevee?", PORTRAIT_DEMIVEE_ANNOYED, PORTRAIT_DEFAULT_LEFT)
			msgbox("The local Pokémon are always testy\n" "after a flood.\p" "It's dangerous for a human to go\n" "alone. Take me!")
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("I can handle myself pretty well.\n" "And I'm a Pokémon Ranger, too.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("But if you want to come along,\n" "I won't say no. Welcome aboard!", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_DEMIVEE, EltheRoad_Movement_DemiveeJoin)
			waitmovement(0)
			removeobject(LOCALID_DEMIVEE)
			playfanfare(MUS_EVOLVED)
			additem(ITEM_LEAFSHARD)
			message("Eevee joined your party!")
			waitmessage
			waitfanfare
			waitbuttonpress
			closemessage
			fadedefaultbgm
			setvar(VAR_MAIN_GAME_STATE, 1)
		}
	]
	MAP_SCRIPT_ON_TRANSITION {
		if(flag(FLAG_TREES_CLEARED))
		{
			setobjectxyperm(LOCALID_LOGAN, 8, 14)
			setobjectmovementtype(LOCALID_LOGAN, MOVEMENT_TYPE_FACE_LEFT)
		}
	}
}	

script EltheRoad_EventScript_FirstTrigger
{
	lock
	delay(30)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	applymovement(OBJ_EVENT_ID_FOLLOWER, Common_Movement_FaceRight)
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("I meant to ask earlier, but, uh,\n" "what are you, exactly?\p" "You look like an Eevee, but those\n" "markings…are they natural?")
	speakername("Eevee?", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Uh, I dunno! I used to be normal,\n" "but I like to eat these crystals…\l" "I think I got these markings after\l" "I started doing that.\p"
			"Now when I touch an evolution stone,\n" "I don't evolve, but…\p"
			"It changes my type, I think.\n" "And my moves.")
	speakername("Amelia", PORTRAIT_AMELIA_SMUG)
	msgbox("So you're some kind of…Demi-Vee?")
	setpartymemberinfo(0) // Sets nickname, metloc, and met level.
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_HUH, PORTRAIT_DEFAULT_LEFT)
	msgbox("Huh?")
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Um, anyway! If you find any cool\n" "crystals, give them to me!\l" "They'll make me stronger in battle.\p"
			"And if you need any advice for\n" "getting around, just ask me!\l" "I know this place pretty well!")
	speakername(NULL, 0)
	msgbox("(Press the Select button or talk to\n" "Demi-Vee to get advice!)\p" "(He can give you hints about how to\n" "progress through the game.)", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 2)
}

script EltheRoad_EventScript_DemiveeTutorialSign
{
	lock
	msgbox("{EMPHASIS_RED}!! NON-DIEGETIC SIGN ALERT !!{EMPHASIS_OFF}\p" "This sign does not really exist!\n" "It is just here to help you, Player.\p"
			"Your new companion is a very\n" "unique Pokémon. He doesn't grow\l" "in the way you're used to.\p"
			"If you want to make the best of his\n" "unique traits, read on.")
	goto(EltheRoad_EventScript_DemiveeTutorialMultichoice)
}

script EltheRoad_EventScript_DemiveeTutorialMultichoice
{
	message("What would you like to read about?")
	waitmessage
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Forms", "Shards", "Leveling Up", "Stat Points", "Learning Skills", "Stop Reading")
	switch(var(VAR_RESULT))
	{
		case 0:
			msgbox("Demi-Vee can't evolve, but he can\n" "change forms using special Shards.\p"
					"Each form matches the type of a\n" "normal evolution and has similar\l" "stats and moves.\p"
					"The four moves he starts with also\n" "change depending on his form.\p"
					"For example, his first move will be\n" "a mid-power move matching his Type.\p"
					"Be careful, though; if you delete\n" "one of these base moves, he will\l" "lose access to it in all forms.\l" "For example, if you delete Razor Leaf\l" "while in Grass form, you will also\l" "lose Shock Wave in Electric form.")					
		case 1:
			msgbox("There are various Shards scattered\n" "throughout this area.\p"
					"They are pieces of Evolution Stones\n" "which allow Demi-Vee to change form.\p"
					"You may find them on the ground, or\n" "get them as rewards for doing favors.\p"
					"You can see your currently owned\n" "shards in the Key Items pocket.\p"
					"To change Demi-Vee's form, open the\n" "POKéMON menu, select Demi-Vee, and\l" "pick the SHARDS option. On this\l" "screen, you can change forms using\l" "the L and R buttons.")
		case 2:
			msgbox("Demi-Vee does not gain Exp. Points\n" "from participating in battle.\p"
					"He gains Exp. Points from eating\n" "Rock Candy instead.\p"
					"Like Shards, these can be found\n" "around town or received as rewards.\p"
					"It can be useful to save them if\n" "you're not planning to fight soon,\l" "as leveling up becomes more valuable\l" "when you have more Shards.")
		case 3:
			msgbox("For each level the Demi-Vee gains,\n" "he earns 4 Stat Points.\p"
					"You can spend them in the Shards menu\n" "to increase his Base Stats.\p"
					"Press the A Button, then use the\n" "Control Pad to assign the points,\l" "and the A Button to save, or the\l" "B Button to cancel.\p"
					"You can't re-allocate points after\n" "spending them, so choose carefully.")
		case 4:
			msgbox("When Demi-Vee levels up, he will\n" "have an opportunity to learn a\l" "skill from the Shard he is using.\p"
					"The first two skills are Moves that\n" "Demi-Vee can learn. Moves learned\l" "in this way can be used no\l" "matter what form he is in.\p"
					"The third skill lets you choose to\n" "permanently replace his Ability.\l" "This, too, persists between forms.\p"
					"You can see the skills each Shard\n" "will teach in the bottom right\l" "corner of the Shards menu.\l" "Skills you have already learned\l" "will be displayed with blue text.\p"
					"Skills cannot be relearned if you\n" "pass on them or delete them.\l" "It may be wise to carefully consider\l" "the skills each Shard can teach\l" "before you use your Rock Candy.")
		case 5:
		case MULTI_B_PRESSED:
			release
			end
	}
	goto(EltheRoad_EventScript_DemiveeTutorialMultichoice)
	
}

script EltheRoad_EventScript_FirstSign
{
	msgbox("{CLEAR_TO 60}{LEFT_ARROW} To Rehocan Town\n" "{CLEAR_TO 76}To Elthe City {RIGHT_ARROW}", MSGBOX_SIGN)
}

script EltheRoad_Trigger_VinosTrigger 
{
	lock
	delay(32)
	special(SpawnCameraObject)
	applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp)
	waitmovement(0)
	delay(32)
	speakername("Vine Mon", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Ugh…stupid flood.\n" "Washed away a perfect lurking spot.", MSGBOX_AUTOCLOSE)
	applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
	waitmovement(0)
	special(RemoveCameraObject)
	delay(32)
	message("The Pokémon hasn't noticed you…\n" "What will you do?")
	waitmessage
	dynmultichoice(0, 0, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Negotiate", "Attack")
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Excuse me…", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
			waitmovement(0)
			applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(48)
			speakername("Vine Mon", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("What? A human speaks to me…")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Do you mind if I pass through here?", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
			waitmovement(0)
			delay(48)
			speakername("Vine Mon", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("You're asking my permission? As if you\n" "can understand my speech?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Yeah, I can. It's kinda my thing.\n" "Let's talk this out.")
			speakername("Vine Mon", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Fascinating…this may be an interesting\n" "exercise. I am Vinos. Let us begin.")
			speakername(NULL, 0)
			msgbox("Try to make Vinos happy so it'll let\n" "you pass through! It will ask you\l" "a series of questions.\p"
					"If you make Vinos angry, it may\n" "decide to attack you instead.\p"
					"If it likes your answers, you may\n" "get a gift as well.", MSGBOX_AUTOCLOSE)
			delay(32)
			goto(EltheRoad_VinosNegotiationMain)
		case 1:
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("Demi-Vee, attack!", MSGBOX_AUTOCLOSE)
			playse(SE_PIN)
			applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
			waitmovement(0)
			applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
			waitmovement(0)
			speakername("Vine Mon", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
			msgbox("What!? You -", MSGBOX_AUTOCLOSE)
			goto(EltheRoad_VinosDoBattle)			
	}
}

// Does a random negotiation topic
script EltheRoad_VinosNegotiationMain
{
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL)
	message("Alright, then…")
	waitmessage
	waitbuttonpress
	setvar(VAR_0x8008, 4)
	special(GetNegotiationTopic) // this sets temp flags
	if (var(VAR_RESULT) == 0xFF) // all topics used up
	{
		goto(EltheRoad_VinosNegotiationMidSuccess)
	}
	else
	{
		switch(var(VAR_RESULT))
		{
			case 0:
				dynmultipush("Dark", 1)
				dynmultipush("Grass", 0)
				dynmultipush("Fire", 2)
				message("Which Type of Pokémon is best,\n" "in your opinion?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, FALSE, 1, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
						msgbox("It's, uh…Grass. Yeah. I just love\n" "Grass type Pokémon…\p" "… … … … …")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("You know what, no, I can't even f{CENSOR}uck{UNCENSOR}ing\n" "joke about it. All the major\l"
						"terrible events in my life were\l" "caused by Grass types.\l" "F{CENSOR}uck{UNCENSOR} Grass types. I hate 'em.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("You'd disrespect me to my face…\n" "You have some nerve, human.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}
						playse(SE_FAILURE)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Oh, Dark is the best, obviously!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Hmmhmm, yes, we are quite…\n" "Wait! You're just buttering me up!")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("No way! I'm being honest! Some of my\n" "best friends are Dark types!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("…Well, alright. It's a good answer.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Hmm…I think it must be Fire.\n" "It's just such a cool element.")
						speakername("Amelia", PORTRAIT_AMELIA_WINK)
						msgbox("Plus, I was a Fire type for a while,\n" "myself. Self love, yo!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("You have some nerve, telling a Grass\n" "type that Fire is best…\p"
								"But I appreciate your honesty.\n" "The time for tricks has passed.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
			case 1:
	// - Question: Eat one of my berries! (Accept: Fail, Question logistics: Mid, Eat trail mix: Success)
				dynmultipush("Your berries?", 1)
				dynmultipush("Eat trail mix", 2)
				dynmultipush("Just eat it", 0)
				message("Would you like to eat\n" "one of my berries?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, FALSE, 1, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						callnative(ParalyzeLead)
						speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL)
						msgbox("Ooh, I'll take one!", MSGBOX_AUTOCLOSE)
						delay(90)
						speakername("Demi-Vee", PORTRAIT_DEMIVEE_ANNOYED)
						msgbox("Urp…")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Ha! What an idiot! You didn't even\n" "fall for my camouflage, but you\l"
								"still ate my paralyzing berries!\l" "You're mine!", MSGBOX_AUTOCLOSE)
						goto(EltheRoad_VinosDoBattle)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Berries? They grow on your body?\n" "And you…WANT us to eat them?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Well…yes. They're delicious.\n" "Or so I'm told.")
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("What about the ones that your eyes\n" "are on? Are they edible?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No, they're…my eyes. Not berries.")
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Has anyone ever tried to pick your\n" "eyes instead of a berry?")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("Look, are you gonna eat a berry\n" "or not!?")
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("Nope.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("… … … … … … … … … …\p" "Okay, forget it. Next question.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Oh, that reminds me I have…")
						speakername("Amelia", PORTRAIT_AMELIA_MUNCH)
						msgbox("Trail mix!")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("… … … … …\p" "Your ways confuse me, human.\n" "I would like to stop talking to you.", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
				
			case 2:
	// - Question: Why do you need to get through? (To get a bath: Fail, Cuz I don't want to fight you: Mid, For thrills: Success)
				dynmultipush("To get my team back", 0)
				dynmultipush("For thrills", 2)
				dynmultipush("I don't want to fight", 1)
				message("Why should I let you through?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, FALSE, 0, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("I need to get my team back! We got\n" "separated by the flood.\p" "I'm their Trainer.\n" "We need each other.")
						speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
						msgbox("Why is that my problem?\n" "I'm hungry.")
						speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
						msgbox("Hungr- were you planning to eat me!?")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No. I was just giving an example of\n" "something that's not your problem.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}					
						playse(SE_FAILURE)	
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("There's no reason for us to fight.\n" "Look at this place, it's a mess.\p"
								"Just let me move on to take care of\n" "my business, and you can get back\l"
								"to finding a new tree, or whatever\l" "it is you're doing.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Pokémon don't need a reason to\n" "fight…but now that you mention it,\l"
								"it does sound like a waste\l" "of time right now…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("I bet you fight every Trainer who\n" "comes through here, right?\p"
								"Wouldn't it be a thrill to let\n" "one pass through, just this once?\l"
								"Get a little crazy with it.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("Hmm…perhaps under the circumstances,\n" "it would serve me better…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
			case 3:
	// - Question: How long have you been training Pokémon? (30 years: mid, just started: fail, long enough: success)
				dynmultipush("Thirty years", 1)
				dynmultipush("Long enough", 2)
				dynmultipush("Just started", 0)
				message("How long has it been since you\n" "started training Pokémon?")
				waitmessage
				dynmultistack(0, 0, FALSE, 6, FALSE, 1, DYN_MULTICHOICE_CB_NONE)
				switch(var(VAR_RESULT))
				{
					case 0:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Hmm, well me and Demi-Vee just met,\n" "so I guess I just started.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("No wonder you tried talking. You\n" "have no idea what you're doing.", MSGBOX_AUTOCLOSE)
						setflag(FLAG_NOT_PERFECT_CONVERSATION)
						if (var(VAR_NEGOTIATION_SCORE) > 0)
						{
							subvar(VAR_NEGOTIATION_SCORE, 1)
						}
						playse(SE_FAILURE)
					case 1:
						speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
						msgbox("Let me think…Python's last birthday\n" "was, uh…\p"
								"Hmm, I can't remember exactly, but\n" "I think I started in the year…\p"
								"So, it must have been at least\n" "thirty years.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("You expect me to believe that?\n" "You don't even look like you're\l"
								"thirty years old. You're just\l" "trying to intimidate me.")
						speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
						msgbox("It's true, though…", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 1)
					case 2:
						speakername("Amelia", PORTRAIT_AMELIA_SMUG)
						msgbox("Long enough.")
						speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
						msgbox("…!", MSGBOX_AUTOCLOSE)
						addvar(VAR_NEGOTIATION_SCORE, 2)
						playse(SE_SUCCESS)
				}
		}
		
		delay(16)
		if (var(VAR_NEGOTIATION_SCORE) >= 3)
		{
			if(flag(FLAG_NOT_PERFECT_CONVERSATION))
			{
				goto(EltheRoad_VinosNegotiationMidSuccess)
			}
			goto(EltheRoad_VinosNegotiationCompleteSuccess)
		}
		elif(var(VAR_NEGOTIATION_SCORE) == 0)
		{
			speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
			msgbox("You're getting on my nerves…\p" "Enough talk! I should have done this\n" "from the beginning!", MSGBOX_AUTOCLOSE)
			goto(EltheRoad_VinosDoBattle)
		}
		else
		{
			goto(EltheRoad_VinosNegotiationMain)
		}
	}
}

script EltheRoad_VinosNegotiationMidSuccess
{
	call(EltheRoad_CleanUpNegotiationValues)
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("…well, I suppose you're not wholly\n" "unpleasant. And I don't feel like\l"
			"jumping you anymore anyway.\l" "You can pass.\p"
			"And here, take this. I have no\n" "use for it anyway.", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_L, 2)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_YAY)
	msgbox("Ooh! A crystal! Yummy!")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Run along, now.")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Okay, then. Thanks for your\n" "patience. Buh-bye!", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 3)
	setflag(FLAG_TEMP_9) // Vinos's dialogue changes if you leave and come back
}

script EltheRoad_VinosNegotiationCompleteSuccess
{
	call(EltheRoad_CleanUpNegotiationValues)
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Hmm. That was a nice change of pace.\n" "Rarely do I speak even to those of\l" "my own kind. Even rarer to speak to\l" "a human as an equal.\p"
			"Here. Take this as a token\n" "of my appreciation.", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_XL, 1)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_YAY)
	msgbox("Ooh! A huge crystal! Yummy!\n" "I'll save it for later!")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("I saw another such crystal a bit further\n" "up the path, behind a tree. A red one.\p"
			"Look for it if it interests you.\n" "Keep yourself safe.")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Okay, thanks for the tip!\n" "See you around, Vinos!", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 3)
	setflag(FLAG_TEMP_8)
}

script EltheRoad_VinosDoBattle
{
	createmon(1, 0, SPECIES_VINOS, 35, ITEM_NONE, 0, NATURE_IMPISH, 0, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_WRAP, MOVE_FEINT_ATTACK, MOVE_GLARE, MOVE_LEER, FALSE, 0, TYPE_WATER)
	dowildbattle
	call(EltheRoad_CleanUpNegotiationValues)
	turnobject(LOCALID_VINOS, DIR_SOUTH)
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("… … … … …\p" "…well? Aren't you going to\n" "finish me off?")
	speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
	msgbox("What? No. I only wanted to get\n" "through here.")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("…Oh.", MSGBOX_AUTOCLOSE)
	delay(60)
	applymovement(LOCALID_VINOS, Common_Movement_FaceLeft)
	delay(120)
	applymovement(LOCALID_VINOS, Common_Movement_FaceRight)
	delay(120)
	applymovement(LOCALID_VINOS, Common_Movement_FaceDown)
	speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
	msgbox("Well, go along then! And if it\n" "will get you to leave me alone,\l" "then take this!", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_L)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_YAY)
	msgbox("Ooh! A crystal! Yummy!")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Those things are all over the\n" "place here. I think I saw another\l" "type of crystal next to that stump\l" "behind me.\p" "Run along, then.", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 3)
	setflag(FLAG_TEMP_9) // Vinos's dialogue changes if you leave and come back
}

script EltheRoad_CleanUpNegotiationValues
{
	setvar(VAR_NEGOTIATION_SCORE, 0)
	clearflag(FLAG_TEMP_4)
	clearflag(FLAG_TEMP_1)
	clearflag(FLAG_TEMP_2)
	clearflag(FLAG_TEMP_3)
	clearflag(FLAG_NOT_PERFECT_CONVERSATION)
	return
}

script EltheRoad_EventScript_VinosIdle
{
	lock
	faceplayer
	if(flag(FLAG_TEMP_9))
	{
		speakername("Vinos", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
		msgbox("If you truly just wanted to pass\n" "by, then hurry up already!\p"
				"I don't want to see your face\n" "right now. Begone.", MSGBOX_AUTOCLOSE)
	}
	elif(!flag(FLAG_FLARESHARD_GET)) // if you don't negotiate perfectly, he won't say this unless you backtrack
	{
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL)
		message("If you want more crystals, there's\n" "one stuck in a tree…")
		waitmessage
		special(SpawnCameraObject)
		specialvar(VAR_RESULT, GetPlayerFacingDirection)
		switch(var(VAR_RESULT))
		{
			case DIR_EAST:
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkRight)
				waitmovement(0)
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp)
				waitmovement(0)
			case DIR_NORTH:
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkUp2)
				waitmovement(0)
		}
		applymovement(OBJ_EVENT_ID_CAMERA, EltheRoad_Movement_CameraShowItem)
		waitmovement(0)
		message("Right here.")
		setmetatile(26, 20, 0x247, FALSE)
		special(DrawWholeMapView)
		delay(16)
		setmetatile(26, 20, 0x20C, FALSE)
		special(DrawWholeMapView)
		delay(16)
		setmetatile(26, 20, 0x247, FALSE)
		special(DrawWholeMapView)
		delay(16)
		setmetatile(26, 20, 0x20C, FALSE)
		special(DrawWholeMapView)
		delay(16)
		setmetatile(26, 20, 0x247, FALSE)
		special(DrawWholeMapView)
		delay(16)
		setmetatile(26, 20, 0x20C, FALSE)
		special(DrawWholeMapView)
		delay(16)
		waitbuttonpress
		closemessage
		applymovement(OBJ_EVENT_ID_CAMERA, EltheRoad_Movement_CameraReturn)
		waitmovement(0)
		specialvar(VAR_RESULT, GetPlayerFacingDirection)
		switch(var(VAR_RESULT))
		{
			case DIR_EAST:
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
				waitmovement(0)
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkLeft)
				waitmovement(0)
			case DIR_NORTH:
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
				waitmovement(0)
				applymovement(OBJ_EVENT_ID_CAMERA, Common_Movement_WalkDown)
				waitmovement(0)
		}
		special(RemoveCameraObject)		
	}
	elif(flag(FLAG_TEMP_8)) // only visible if you negotiate perfectly and haven't left the map yet 
	{
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("How exactly are you able to\n" "understand my speech?")
		speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
		msgbox("Oh, it's a long story…\n" "A long, deeply stupid story…\p"
				"My head's a bit hazy on the details\n" "right now. I think it starts with me\l" "kidnapping a Pokémon from a Professor,\l" "and then at some point my arm fell\l" "off, and I got stuck in the body of a…\p"
				"You know, I'd really rather not\n" "get into it.\p"
				"The short version is that it's a\n" "highly unique circumstance.")
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("I see…so no other humans have\n" "this ability?")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("I think some do, but…not the same\n" "kind as me.")
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("How curious.", MSGBOX_AUTOCLOSE)
	}
	else
	{
		speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
		msgbox("Normally, I try to disguise myself\n" "as a grapevine.\p"
				"If anyone is foolish enough to eat\n" "of my fruit, they'll be paralyzed\l" "soon enough to become my prey.\p"
				"It's just poor fortune that I've\n" "been left high and dry, so to speak,\l" "by this unbearable downpour.\p"
				"I can't even recall where my\n" "usual lurking spot is.", MSGBOX_NPC)
	}
	release
}

script EltheRoad_Trigger_VinosAltTrigger
{
	lock
	playse(SE_PIN)
	applymovement(LOCALID_VINOS, Common_Movement_FaceLeft)
	waitmovement(0)
	applymovement(LOCALID_VINOS, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	speakername("Vine Mon", PORTRAIT_VINOS_MAD, PORTRAIT_DEFAULT_LEFT)
	msgbox("Ack! A trainer!?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Hi. I just wanted to sneak by you.\n" "Do you mind?")
	speakername("Vine Mon", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("You're…asking my permission? What?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Yeah. Don't wanna be rude.\n" "What kind of Pokémon are you?\l" "I've never seen this kind before.")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("…I am called Vinos.\n" "…Apologies, I am very confused.\p"
			"I have never seen a human interact\n" "with a Pokémon in this way. Usually\l" "they just attack. And I thought\l" "humans couldn't hear our speech.")
	speakername("Amelia", PORTRAIT_AMELIA_SMUG)
	msgbox("Well, most can't. But I can.")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("Fascinating. A gift, then, for one\n" "who has shown me something new.", MSGBOX_AUTOCLOSE)
	giveitem(ITEM_EXP_CANDY_XL, 1)
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_YAY)
	msgbox("Ooh! A huge crystal! Yummy!\n" "I'll save it for later!")
	speakername("Vinos", PORTRAIT_VINOS_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
	msgbox("I saw another such crystal a bit further\n" "up the path, behind a tree. A red one.\p"
			"Look for it if it interests you.\n" "Keep yourself safe.")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Okay, thanks for the tip!\n" "See you around, Vinos!", MSGBOX_AUTOCLOSE)
	setvar(VAR_MAIN_GAME_STATE, 3)
	setflag(FLAG_TEMP_8)
}

script EltheRoad_Trigger_Marcel_Right
{
	lock
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
	msgbox("Hey! You! Kingler!", MSGBOX_AUTOCLOSE)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaGoToMarcel)
	applymovement(OBJ_EVENT_ID_FOLLOWER, EltheRoad_Movement_DemiveeGoToMarcel_Right)
	applymovement(LOCALID_MARCEL, Common_Movement_QuestionMark)
	waitmovement(LOCALID_MARCEL)
	applymovement(LOCALID_MARCEL, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(LOCALID_MARCEL)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	goto(EltheRoad_Trigger_MarcelMain)
}

script EltheRoad_Trigger_Marcel_Left
{
	lock
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
	msgbox("Hey! You! Kingler!", MSGBOX_AUTOCLOSE)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaGoToMarcel)
	applymovement(OBJ_EVENT_ID_FOLLOWER, EltheRoad_Movement_DemiveeGoToMarcel_Left)
	applymovement(LOCALID_MARCEL, Common_Movement_QuestionMark)
	waitmovement(LOCALID_MARCEL)
	applymovement(LOCALID_MARCEL, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(LOCALID_MARCEL)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	goto(EltheRoad_Trigger_MarcelMain)
}

script EltheRoad_Trigger_MarcelMain
{
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("You're…you are…\n" "What's your name again?")
	speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
	msgbox("I'm Marcel. I think I know you.\n" "Do you know me?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("I guess so. I have a Poké Ball here\n" "with your name on it.")
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_HUH)
	msgbox("Is this one of your team members?\n" "Why don't you, like, know each other?")
	speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
	msgbox("I haven't been able to think straight\n" "ever since the flood…")
	applymovement(LOCALID_MARCEL, Common_Movement_WalkRight)
	speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
	msgbox("Oh, who's this cute little thing?")
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL)
	msgbox("I'm the Demi-Vee! I'm tagging along\n" "to keep Amelia out of trouble!")
	speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
	msgbox("As if! Nobody keeps me out of trouble!")
	speakername("Marcel", PORTRAIT_KINGLER_LAUGH)
	msgbox("Heh heh heh! That sounds right to me!\n" "I remember Vice told me about that\l" "time when your ex-boyfriend…", MSGBOX_AUTOCLOSE)
	playse(SE_PIN)
	applymovement(LOCALID_MARCEL, Common_Movement_ExclamationMark)
	waitmovement(0)
	delay(48)
	applymovement(LOCALID_MARCEL, EltheRoad_Movement_MarcelRunToAmelia)
	waitmovement(0)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	applymovement(OBJ_EVENT_ID_FOLLOWER, EltheRoad_Movement_DemiveeRunFromMarcel)
	playse(SE_LEDGE)
	speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
	msgbox("Vice! That's it, Amelia! You need\n" "to help me find my girlfriend!")
	speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
	msgbox("Of course! How could I forget about\n" "my favorite battle couple!")
	speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
	msgbox("There's a town nearby. She hates\n" "rain, so she's probably holed up\l" "inside somewhere. Let's go!", MSGBOX_AUTOCLOSE)
	// animation for amelia putting the mon in a ball?
	removeitem(ITEM_GREAT_BALL, 1)
	delay(32)
	applymovement(LOCALID_MARCEL, EnterPokeballMovement)
	waitmovement(0)
	removeobject(LOCALID_MARCEL)
	delay(16)
	playfanfare(MUS_EVOLVED)
	message("You recalled Marcel the Kingler!")
	waitmessage
	waitfanfare
	waitbuttonpress
	closemessage
	givemon(SPECIES_KINGLER, 50, ITEM_SPLASH_PLATE, ITEM_GREAT_BALL, NATURE_QUIET, 1, MON_FEMALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_LIQUIDATION, MOVE_BODY_SLAM, MOVE_GUILLOTINE, MOVE_HAMMER_ARM, FALSE, FALSE, TYPE_DRAGON)
	setpartymemberinfo(1)
	setvar(VAR_MAIN_GAME_STATE, 4)
}

script EltheRoad_EventScript_TreeCleanupMan
{
	if (flag(FLAG_TREES_CLEARED))
	{
		speakername("Logan")
		msgbox("There's something unsettling about\n" "the path behind these branches…\p" "Maybe I should just leave these\n" "where they are…", MSGBOX_NPC)
	}
	else
	{
		speakername("Logan")
		msgbox("Hail, traveler! I'm working on\nclearing out all these branches.\p" "You should be able to make it to the\n" "city now. I'll get the road all clear\l" "as soon as I can.", MSGBOX_NPC)
	}
}

script EltheRoad_EventScript_BerryFieldsSign
{
	msgbox("Elthe City Berry Fields\p" "Please see overseer before picking\n" "any berries!", MSGBOX_SIGN)
}

script EltheRoad_BerrySpot_WeakBerries
{
	lock
	msgbox("Some berries are floating in the\n" "water…grab one?", MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES)
	{
		msgbox("You plunged your hand into the water…")
		delay(32)
		random(3)
		switch(var(VAR_RESULT))
		{
			case 0:
				giveitem(ITEM_ORAN_BERRY)
			case 1:
				giveitem(ITEM_CHERI_BERRY)
			case 2:
				giveitem(ITEM_PERSIM_BERRY)
		}
	}
	release
}

script EltheRoad_BerrySpot_StrongBerries
{
	lock
	msgbox("Some berries are floating in the\n" "water…grab one?", MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES)
	{
		msgbox("You plunged your hand into the water…")
		delay(32)
		random(3)
		switch(var(VAR_RESULT))
		{
			case 0:
				giveitem(ITEM_SITRUS_BERRY)
			case 1:
				giveitem(ITEM_LUM_BERRY)
			case 2:
				giveitem(ITEM_LEPPA_BERRY)
		}
	}
	release

}


script EltheRoad_BerrySpot_PinchBerries
{
	lock
	msgbox("Some berries are floating in the\n" "water…grab one?", MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES)
	{
		msgbox("You plunged your hand into the water…")
		delay(32)
		random(3)
		switch(var(VAR_RESULT))
		{
			case 0:
				giveitem(ITEM_LIECHI_BERRY)
			case 1:
				giveitem(ITEM_SALAC_BERRY)
			case 2:
				giveitem(ITEM_PETAYA_BERRY)
		}
	}
	release

}


script EltheRoad_BerrySpot_PlotBerries
{
	lock
	msgbox("Some berries are floating in the\n" "water…grab one?", MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES)
	{
		msgbox("You plunged your hand into the water…")
		delay(32)
		random(4)
		switch(var(VAR_RESULT))
		{
			case 0:
				giveitem(ITEM_SPELON_BERRY)
			case 1:
				giveitem(ITEM_WATMEL_BERRY)
			case 2:
				giveitem(ITEM_BELUE_BERRY)
			case 3:
				giveitem(ITEM_SPELON_BERRY)
		}
	}
	release

}

script EltheRoad_EventScript_ExpertTrainer
{
	trainerbattle_single(TRAINER_EDWARD, "Excuse me, dear, could I trouble you\n" "for a Pokémon battle?", "Ohoho, that's nice.")
	msgbox("Leavanny gets antsy if she doesn't\n" "battle often enough. If she gets too\l" "stressed, she eats the berries.", MSGBOX_AUTOCLOSE)
}

script EltheRoad_Trigger_AromaLadyTrainer
{
	lock
	setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
	applymovement(OBJ_EVENT_ID_PLAYER, EltheRoad_Movement_AmeliaGoToAromaLady)
	applymovement(OBJ_EVENT_ID_FOLLOWER, EltheRoad_Movement_FollowerFollowBerry)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Excuse me, do you mind if I take\n" "some of these berries?")
	applymovement(12, Common_Movement_FaceLeft)
	speakername("Amelia")
	msgbox("Hello, who are you?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("My name's Amelia. I'm a Pokémon Ranger.\n" "I got a little lost, and I need some\l" "berries to get my team back together.\l" "We were separated by the flood.")
	speakername("Amelia")
	msgbox("Wow! My name's also Amelia!\n" "I'm a gardener.\p"
			"You can take some of these berries.\n" "But would you mind having a battle\l" "with me first?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Oh, sure. No problem.", MSGBOX_AUTOCLOSE)
	trainerbattle_no_intro(TRAINER_CELINA, "I guess you're the stronger Amelia!")
	speakername("Amelia")
	msgbox("Alright, thanks, Amelia! That was\n" "fun. I won't bother you anymore.\p"
			"Actually, I want to give you a gift.\n" "Here, take this.")
	speakername(NULL)
	giveitem(ITEM_VAPORSHARD)
	setflag(FLAG_VAPORSHARD_GET)
	speakername("Amelia")
	msgbox("I don't know what it's for, but it's\n" "pretty, right?")
	speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
	msgbox("Yeah. Thanks, this will be useful.\n" "See you around.")
	setvar(VAR_BERRY_FIELD_STATE, 1)
	setvar(VAR_MAIN_GAME_STATE, 10)
	release
}

script EltheRoad_EventScript_AromaLady
{	
	lock
	faceplayer
	speakername("Amelia")
	if(!flag(FLAG_GOT_VICE))
	{
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("I'm trying to find some berries to\n" "make a spicy treat at Pomona Bread.\l" "What kind should I get?")
		speakername("Amelia")
		msgbox("Oh, the spiciest berries we grow here\n" "are Spelon berries! There should be\l" "some right in front of me.\l" "Just reach in the water and grab them.")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("Thanks, Amelia.")
	}
	else
	{
		msgbox("Berries are hardy enough that I\n" "think we'll be able to just replant\l" "them once the flooding clears up.\l" "But still, it's a bother.")
	}
	release
}

script EltheRoad_EventScript_BerryMan
{
	speakername("Barry")
	msgbox("The flood's completely demolished\n" "our berry fields. We'll have to\l" "replant whatever we can salvage once\l" "this dries up.\p"
			"If you want to try dredging up some\n" "berries for yourself, you should ask\l" "Amelia about it. She's down by the\l" "other end of this plot.", MSGBOX_NPC)
}

movement EltheRoad_Movement_AmeliaGoToAromaLady
{
	walk_down * 2
	walk_right
}

movement EltheRoad_Movement_FollowerFollowBerry
{
	walk_down * 3
	face_right
}

movement EltheRoad_Movement_DemiveeRunFromMarcel
{
	lock_facing_direction
	jump_down
	unlock_facing_direction
	delay_16 * 2
	walk_fast_left * 2
	walk_fast_up
	face_right
}

movement EltheRoad_Movement_MarcelRunToAmelia
{
	walk_fast_down * 2
}

movement EltheRoad_Movement_AmeliaGoToMarcel
{
	walk_fast_up * 2
}

movement EltheRoad_Movement_DemiveeGoToMarcel_Left
{
	walk_slow_up * 2
	walk_slow_right
	walk_slow_up
}

movement EltheRoad_Movement_DemiveeGoToMarcel_Right
{
	walk_slow_up * 2
}

movement EltheRoad_Movement_CameraShowItem
{
	walk_up * 3
	walk_right
}

movement EltheRoad_Movement_CameraReturn
{
	walk_left
	walk_down * 3
}

movement EltheRoad_Movement_AmeliaJumpUp
{
	set_visible
	jump_in_place_left
}

movement EltheRoad_Movement_AmeliaLookAround
{
	face_up
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_down
	delay_16
	face_right
	delay_16 * 3
	face_left
	delay_16 * 3
}

movement EltheRoad_Movement_AmeliaStartLeaving
{
	walk_up * 2
	walk_right * 3
	delay_16 * 2
	face_left
}

movement EltheRoad_Movement_DemiveeFollow
{
	jump_up
	walk_fast_up
	walk_fast_right * 2
}

movement EltheRoad_Movement_DemiveeJoin
{
	walk_slow_right
	enter_pokeball
}

movement EltheRoad_Movement_VinosGetOnLog
{
	walk_fast_right
	jump_right
	face_down
}
