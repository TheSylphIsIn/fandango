const LOCALID_AMELIA=2
const LOCALID_NIDOKING=1

mapscripts IntroScene_MapScripts
{
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_TEMP_0, 0 {
			applymovement(LOCALID_AMELIA, IntroScene_Movement_AmeliaSnooze)
			waitmovement(0)
			playse(SE_RAIN)
			delay(60)
			applymovement(LOCALID_NIDOKING, Common_Movement_QuestionMark)
			waitmovement(0)
			delay(60)
			speakername("Nidoking", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_X, PORTRAIT_DEFAULT_Y, TRUE)
			msgbox("Hmm? I hear something…", MSGBOX_AUTOCLOSE)
			applymovement(LOCALID_NIDOKING, IntroScene_Movement_BradleyInvestigate)
			waitmovement(0)
			speakername("Nidoking", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_X, PORTRAIT_DEFAULT_Y, TRUE)
			msgbox("Amelia, I hear a strange noise…\n" "It sounds like rushing water.", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_ASLEEP)
			msgbox("No, Bradley, I just got comfortable…\n" "Don't talk to me about rushing water.\p" "It's probably nothing.", MSGBOX_DEFAULT)
			speakername("Bradley", PORTRAIT_NIDOKING_NERVOUS, PORTRAIT_DEFAULT_X, PORTRAIT_DEFAULT_Y, TRUE)
			msgbox("I'm on watch! It's my job to talk\n" "to you about these things.\p" "It's getting louder…", MSGBOX_DEFAULT)
			speakername("Amelia", PORTRAIT_AMELIA_ASLEEP)
			msgbox("Unless it's a flash flood, we can\n" "just deal with it in the morning -", MSGBOX_AUTOCLOSE)
			playse(SE_M_HYDRO_PUMP)
			fadescreen(FADE_TO_WHITE)
			delay(60)
			setflag(FLAG_SPAWN_INVISIBLE)
			setflag(FLAG_REMOVE_WARP_FADE)
			setflag(FLAG_NO_MUSIC_ON_WARP)
			setflag(FLAG_SYS_POKEMON_GET)
			givemon(SPECIES_DEMIVEE_GRASS, 30, ITEM_EXP_CANDY_L, ITEM_POKE_BALL, NATURE_QUIRKY, 0, MON_MALE, 0, 0, 0, 0, 0, 0, 31, 31, 31, 31, 31, 31, MOVE_RAZOR_LEAF, MOVE_SLASH, MOVE_TAIL_WHIP, MOVE_FURY_CUTTER, FALSE, FALSE, TYPE_DRAGON)
			setvar(VAR_TEMP_0, 1)
			warpsilent(MAP_ELTHE_ROAD, 0, -1, -1)
		}
	]
}

script Test_MonPic
{
	msgbox("message when no tags", MSGBOX_AUTOCLOSE)
	delay(30)
	speakername("oenis", PORTRAIT_FLYGON_NEUTRAL, 20, 5, TRUE)
	msgbox("message when haves tags", MSGBOX_AUTOCLOSE)
	delay(30)
	msgbox("message when no tags", MSGBOX_AUTOCLOSE)	
}

script Test_Portrait
{
	speakername("h", PORTRAIT_KINGLER_NEUTRAL, 21, 6, TRUE)
	waitbuttonpress
	msgbox(format("It will show up when the message box appears."), MSGBOX_DEFAULT)
	speakername("b", PORTRAIT_NIDOKING_NEUTRAL)
	msgbox(format("It can change mid-dialogue."), MSGBOX_DEFAULT)
	speakername("h", PORTRAIT_NONE)
	msgbox(format("Or even disappear."), MSGBOX_DEFAULT)
	speakername(NULL, PORTRAIT_AMELIA_NEUTRAL)
	msgbox(format("Portrait and name can be controlled separately too."), MSGBOX_AUTOCLOSE)
}

movement IntroScene_Movement_AmeliaSnooze
{
	delay_16 * 3
	face_up
	delay_16 * 3
	face_down
	delay_16 * 3
	face_up
	delay_16 * 3
	face_down
	delay_16 * 3
	face_up
	delay_16 * 3
	face_down
	delay_16 * 3
	face_up
	delay_16 * 3
	face_down
}

movement IntroScene_Movement_BradleyInvestigate
{
	walk_slow_down
	delay_16 * 2
	face_right
	delay_16 * 3
	face_left
	delay_16 * 3
	walk_slow_left
	delay_16 * 2
	face_down
	delay_16 * 5
	face_right
}

// interaction scripts for followers. (Demi-Vee's can also be activated by pressing R at any time)
// reference current map with VAR_RESULT

script FollowerScript_Demivee
{
	lock
	copyvar(VAR_TEMP_0, VAR_RESULT)
	special(GetLeadMonSpecies)
	if(var(VAR_RESULT) != SPECIES_EEVEE)
	{
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		setvar(VAR_0x8004, ANIM_GET_OUT_POKEBALL_DOWN)
		callnative(DoAmeliaOverworldAnim)
		delay(32)
	}
	else
	{
		faceplayer
	}
	speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL)
	
	dynmultipush("What to do", 0) // gives hints based on VAR_MAIN_GAME_STATE
	dynmultipush("Resting my team", 1) // tells you about the inn
	dynmultipush("Tattle this area", 2)	// add other options here based on sidequest and/or map location

	message("What did you want to ask about?")
	waitmessage
	dynmultistack(0, 0, FALSE, 6, FALSE, 0, DYN_MULTICHOICE_CB_NONE)
	switch(var(VAR_RESULT))
	{
		case 0:
			switch(var(VAR_MAIN_GAME_STATE))
			{
				case 1:
					speakername("Demi-Vee", PORTRAIT_DEMIVEE_HUH)
					msgbox("Hey, you're not supposed to know\n" "about this feature yet!")
					speakername("Demi-Vee", PORTRAIT_DEMIVEE_NEUTRAL)
					msgbox("Well, then, you can have a little\n" "secret info…\p"
							"After I decide how you trigger\n" "the superboss, anyway.")
				case 2:
				case 3:
					msgbox("Just keep moving forward! If your\n" "team members are still around this\l" "area, they'll probably gather in\l" "the city up ahead.")
				case 4:
					msgbox("Hey, we found one! That's a good\n" "sign that the others are here.\l" "Keep heading toward Elthe City.")
				case 5:
					msgbox("Marcel said Vice would hide from the\n" "rain, right? Let's check inside\l" "these buildings.")
				case 6:
					msgbox("So Vice was in the Bakery…but she\n" "seems a bit pissed off. I'm not sure\l" "she'll want to just come with us.")
				case 7:
					msgbox("We need to know what kind of treat\n" "Vice likes so we can get her one.\p" "Do you think maybe Marcel would know?\n" "She said Vice was her girlfriend.\l" "I wouldn't be surprised if she forgot,\l" "but it's worth a shot, isn't it?")
				case 8:
					msgbox("Alright, let's ask the bakers to\n" "make us a spicy treat for Vice.")
				case 9: 
					msgbox("So we need to go back to Road to Elthe\n" "and head east. I think I saw some\l" "stumps we could jump across, near\l" "where we met Marcel.")
				case 10:
					msgbox("We need to find 3 of a berry that's\n" "high in Spicy and low in other flavors.\p" "Don't forget to check the berry tag!")
				case 15: // has vice, bradley, or both
					if(!flag(FLAG_GOT_VICE))
					{
						msgbox("Have you checked the businesses near\n" "the entrance to town? Vice might have\l" "taken shelter as soon as she could.")
					}
					elif(!flag(FLAG_GOT_BRADLEY))
					{
						msgbox("Did you try fighting in the arena\n" "at the north end of town?\p"
								"If you're having trouble winning, try\n" "exploring to find more Rock Candy\l" "to power me up and counter what the\l" "foes will use.")
					}
					else
					{
						msgbox("Your last missing friend is in\n" "disguise somewhere in town.\p"
								"Have Bradley follow you and try to\n" "pick out the person who isn't\l" "normally there.")
					}
				case 30: // has python
					msgbox("You've got your team back together,\n" "but you still need to help the town.\p"
							"Python said you could clear up the\n" "rain if you confront whatever is\l" "lurking in the shrine on the north\l" "end of town.")
				default:
					msgbox("Huh? I didn't know you could reach\n" "this game state! My mistake!")
			}
		case 1:
			if (flag(FLAG_FINISHED_WEATHERBANE_QUEST))
			{
				msgbox("We can rest at the inn. Our room is\n" "on the right, up the stairs.")
			}
			elif(flag(FLAG_BEAT_WEATHERBANE))
			{
				msgbox("Now that we've cleared up the issue,\n" "we can get our room key from the\l" "front desk at the inn.")
			}
			elif(flag(FLAG_STARTED_WEATHERBANE_QUEST))
			{
				msgbox("We can get a room key if we quiet\n" "down the weathervane. Climb up the\l" "ladder on the outside of the inn.")
			}
			else
			{
				msgbox("There's an inn in town. If you need\n" "to rest, you could try to get a room.")
			}
		case 2:
			switch(var(VAR_TEMP_0))
			{
				case MAPSEC_LITTLEROOT_TOWN:
					speakername("Demi-Vee", PORTRAIT_DEMIVEE_HUH)
					msgbox("Huh?? How'd you get back here?")
				case MAPSEC_OLDALE_TOWN:
					msgbox("This is the road to Elthe City.\n" "It normally looks a bit nicer than\l" "this, but the flood and constant rain\l" "made a mess of it.\p"
							"On the east side of this road are\n" "the city's Berry fields. But they've\l" "been flooded, too. I guess we could\l" "try scavenging in the pools anyway.")
				case MAPSEC_DEWFORD_TOWN:
					msgbox("This is Elthe City.\p"
							"It's been raining here for as long\n" "as anyone can remember.\p"
							"In the southwest are the inn\n" "and the bakery.\p"
							"The east side is a residential area.\n" "There's also a battle arena there.\p"
							"In the northwest is an Arcean church.\n" "I never go there.\p"
							"It's kind of gloomy around here…\n" "Maybe there's something we can do…\l" "What do you think, Amelia?")
				case MAPSEC_ROUTE_101:
					msgbox("This is the Elysi Inn. It's a motel\n" "for people passing through here.\p"
							"If you need to heal us after battle,\n" "you could ask for a room here.")
				case MAPSEC_ROUTE_102:
					msgbox("This is Pomona Bread, the bakery.\n" "They make all sorts of baked goods.\p"
							"I started eating crystals after the\n" "people here caught me stealing from\l" "the displays, haha. But the joke's\l" "on them, because I was controlling the\l" "Pantom population. Now their kitchen\l" "gets haunted once in a while.\p"
							"But if you ask me, there's nothing\n" "wrong with haunted bread.")
				default:
					msgbox("Sorry, I forgot to write an entry\n" "for this area, ahaha...")
			}
	}
	
	release
	special(GetLeadMonSpecies)
	if(var(VAR_RESULT) != SPECIES_EEVEE)
	{
		setvar(VAR_0x8004, ANIM_PUT_AWAY_POKEBALL_DOWN)
		callnative(DoAmeliaOverworldAnim)
		delay(32)
	}	
}

script FollowerScript_Marcel
{
	if(var(VAR_MAIN_GAME_STATE) > 6 && !flag(FLAG_KNOW_VICE_FLAVOR))
	{
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("Hey, Marcel, do you know what Vice's\n" "favorite flavor is?")
		speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
		msgbox("Yeah, she loves spicy things. She\n" "would always eat the Spelon Berries\l" "off of the plant before you could\l" "even pick them.")
		speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
		msgbox("Oh. Cool. That was easy.", MSGBOX_AUTOCLOSE)
		setflag(FLAG_KNOW_VICE_FLAVOR)
	}
	else
	{
		random(3)
		switch(var(VAR_RESULT))
		{
			case 0:
				speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
				msgbox("How did you and Vice get together?")
				speakername("Marcel", PORTRAIT_KINGLER_NEUTRAL)
				msgbox("I knew Guillotine…\n" "She knew Outrage…\l" "Can I make it any more obvious?")
				speakername("Amelia", PORTRAIT_AMELIA_SMUG)
				msgbox("Ah, yes, incredible violence. The\n" "foundation of any good relationship.")
				speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
				msgbox("…That came out wrong.", MSGBOX_AUTOCLOSE)
			case 1:
				
			case 2:
		}
	
	}
}

script FollowerScript_Vice
{
	random(3)
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Vice, do you remember Valora?")
			speakername("Vice", PORTRAIT_FLYGON_NEUTRAL)
			msgbox("Valora, Valora, Valora…hmm, was she\n" "a Bellsprout?")
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("Yeah, I think so. I'm having trouble\n" "remembering anything else about her.\p"
					"And since I know this memory thing\n" "suppresses bad memories…I wonder if\l" "something bad happened to her.")
			speakername("Vice", PORTRAIT_FLYGON_NEUTRAL)
			msgbox("I see…\p"
					"I don't think you should worry about\n" "it, Amelia. It's not like you're\l" "getting careless. Somebody is\l" "ACTIVELY hurting your memory.")
			speakername("Amelia", PORTRAIT_AMELIA_STRAINED)
			msgbox("I guess you're right…I still feel\n" "guilty, though.")
			speakername("Vice", PORTRAIT_FLYGON_NEUTRAL)
			msgbox("Don't! I'll bite you!", MSGBOX_AUTOCLOSE)
		case 1:
		case 2:
	}
}

script FollowerScript_Bradley
{
	random(3)
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Do you remember our first real battle?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Hmm…it wasn't in Alola, was it?\n" "Alola is a blur to me.")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("No, it was in Johto. Back then, you\n" "visited my hiding place by chance,\l" "and your Dunsparce convinced me to\l" "act as a sparring partner.\p"
					"Then we were attacked in the night\n" "by a strange creature. Did you ever\l" "find out why it targeted us?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Hm? Oh, yes, I remember. That was\n" "the night that…")
			speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
			msgbox("The night that I…\p" "Yeah, I think that was my fault.\n" "I, uh, attracted it somehow.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("You know, that thing turned out to be\n" "the guy that Sundown was looking for\l" "the whole time? Crazy coincidence that\l" "it attacked us before we even knew.")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("Indeed. But I just wanted to say, the\n" "leadership you showed in that fight is\l" "the reason that I accepted your offer\l" "to train me.\p"
					"You kept your head in a dangerous\n" "situation and kept everyone safe.")
			speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
			msgbox("It was sheer luck that my plan even\n" "worked…but thanks, Bradley.", MSGBOX_AUTOCLOSE)
		case 1:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Do you know what happened to your\n" "old trainer?")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("No. And I do not care.\p" "He said he would reform his ways.\n" "I commend him if he truly does, but\l" "I still won't ever forgive him.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I see. …I have someone like that, too.\n" "I mean, it's a little different…\l" "I was his trainer instead of him\l" "being mine, but it's like…\p"
					"Well right now I can't remember\n" "exactly what he did, but I still can't\l" "stand him, so it must have been bad.")
			speakername("Bradley", PORTRAIT_NIDOKING_NERVOUS, PORTRAIT_DEFAULT_LEFT)
			msgbox("… … … … …\n" "Of course.", MSGBOX_AUTOCLOSE)
		case 2:
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("I'm sorry again for causing trouble.\p" "I felt like I was back in my younger\n" "days, and I just wanted to rampage.\p"
					"It felt good, but it's not…helpful.")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("It's okay. It's fine to let yourself\n" "have fun once in a while.")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("It's not just that. I lowered my\n" "standards…\p"
					"You know, when I first started with\n" "my old trainer, I did whatever he\l" "told me to. I knew it was wrong, but\l" "it felt good to be a beast.\p"
					"Because of me, more Pokémon suffered…")
			speakername("Amelia", PORTRAIT_AMELIA_SHOCK)
			msgbox("Hey, don't blame yourself for that\n" "piece of s{CENSOR}hit{UNCENSOR}'s business.\p" "It's in the past, alright?")
			speakername("Bradley", PORTRAIT_NIDOKING_NEUTRAL, PORTRAIT_DEFAULT_LEFT)
			msgbox("…", MSGBOX_AUTOCLOSE)
	}
}

script FollowerScript_Python
{
	random(3)
	switch(var(VAR_RESULT))
	{
		case 0:
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("So you still remember everything?")
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("Yup! I'm a Dark type illusionist.\n" "No Psychic type would be able to\l" "put the mind whammy on me!")
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("Okay, so refresh my memory. When that\n" "Hatterene got the jump on us that\l" "time in the woods, you were…?")
			speakername("Python", PORTRAIT_ZOROARK_NERVOUS)
			msgbox("…Holding back of my own free will!\n" "I thought we could deescelate the\l" "situation, you see, if we only\l" "identified ourselves.")
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("And you disguised yourself as a\n" "Grimmsnarl because…")
			speakername("Python", PORTRAIT_ZOROARK_NERVOUS)
			msgbox("A diplomatic move, of course.\n" "(How do you remember this??)")
			speakername("Amelia", PORTRAIT_AMELIA_SMUG)
			msgbox("Uh huh. Of course. Got it.", MSGBOX_AUTOCLOSE)
		case 1:
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("So how much do you remember?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("I still have a general idea of how\n" "we got here and everything, but when\l" "I try to remember specific events,\l" "it makes my head hurt.\p"
					"It's easier to recall good memories\n" "than bad ones.")
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("Mmhmm. So you still remember how\n" "you caught me?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("Yup, you were practically fresh out\n" "of the egg.")
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("How about meeting back up with\n" "your dad?")
			speakername("Amelia", PORTRAIT_AMELIA_NEUTRAL)
			msgbox("It was great once I figured out he\n" "wasn't serious about being hired\l" "to kill me. …Only inconvenience me.")
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("And how {CENSOR}you lost your virginity{UNCENSOR}?")
			speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
			msgbox("…You're too young to be asking that.")
			speakername("Python", PORTRAIT_ZOROARK_LAUGH)
			msgbox("Soooorryyy! I just wanted to see\n" "if you'd slip up.", MSGBOX_AUTOCLOSE)
		case 2:
			speakername("Python", PORTRAIT_ZOROARK_LAUGH)
			msgbox("The people in this town are kinda\n" "funny. They just wander back and forth\l" "between the same places all day.")
			speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
			msgbox("It's not funny, it's sad. They're\n" "basically hypnotized. That's why\l" "we're trying to fix it.")
			speakername("Python", PORTRAIT_ZOROARK_LAUGH)
			msgbox("It's kinda funny. Imagine doing the\n" "same thing on loop every day and\l" "never questioning it.")
			speakername("Amelia", PORTRAIT_AMELIA_SERIOUSLY)
			msgbox("I don't wanna imagine it, that's why\n" "I became a Pokémon Trainer instead\l" "of going to college.")
			speakername("Python", PORTRAIT_ZOROARK_NEUTRAL)
			msgbox("…Damn.", MSGBOX_AUTOCLOSE)
	}
}

