const LOCALID_MORGAN = 3
const LOCALID_CALVIN = 2
const LOCALID_SLOSHMOSH = 5
const LOCALID_ASLEEP_MORGAN = 6
const LOCALID_ASLEEP_PLAYER = 7
const LOCALID_ASLEEP_CALVIN = 8

mapscripts DropperPoint_MapScripts
{
	MAP_SCRIPT_ON_FRAME_TABLE [
			VAR_FONEI_COAST_STATE, 0 {
				lockall
				delay(60)
				setvar(VAR_LAST_TALKED, LOCALID_ASLEEP_PLAYER)
				call(DropperPoint_EventScript_WakeRival)
				applymovement(OBJ_EVENT_ID_PLAYER, DropperPoint_Movement_WakeJump)
				waitmovement(0)
				setvar(VAR_FONEI_COAST_STATE, 1)
				releaseall
			}
		]
}

script DropperPoint_Sign
{
	msgbox("{EMPHASIS_BLUE}DROPPER POINT{EMPHASIS_OFF}\n" "“Pick up a fresh start.”", MSGBOX_SIGN)
}

script DropperPoint_EventScript_WakeRival
{
	applymovement(VAR_LAST_TALKED, DropperPoint_Movement_WakeUp)
	waitmovement(0)
	playse(SE_PIN)
	applymovement(VAR_LAST_TALKED, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(VAR_LAST_TALKED, Common_Movement_Delay48)
	waitmovement(0)
	playse(SE_BIKE_HOP)
	removeobject(VAR_LAST_TALKED)
	return	
}

script DropperPoint_CallScript_RivalGreetPlayer
{
	closemessage
	call(DropperPoint_EventScript_WakeRival)
	addobject(VAR_TEMP_1)
	applymovement(VAR_TEMP_1, DropperPoint_Movement_WakeJump)
	waitmovement(0)
	applymovement(VAR_TEMP_1, Common_Movement_FacePlayer)
	waitmovement(0)
	return
}

script DropperPoint_EventScript_Morgan
{
	lock
	msgbox("Mmm…\n" "Huh…?", MSGBOX_DEFAULT)
	setvar(VAR_TEMP_1, LOCALID_MORGAN)
	call(DropperPoint_CallScript_RivalGreetPlayer)
	clearflag(FLAG_HIDE_MORGAN_INTRO)
	msgbox("Huh? Where am I!? Who are you?\p" "…{PLAYER}? Wait, I think I know you…\n" "We go to the same university.\p" 
			"I sit behind you sometimes…\n" "You might not know me.\p" "My name's Morgan.\n" "…do you know where we are?", MSGBOX_DEFAULT)
	setflag(FLAG_FAVORED_MORGAN)
	release
}

script DropperPoint_EventScript_MorganAwake
{
	speakername(SpeakerName_Morgan)
	if(var(VAR_FONEI_COAST_STATE) == 2)
	{
		msgbox("W-what is that thing!?\n" "Do something, {PLAYER}!", MSGBOX_AUTOCLOSE)
	}
	else 
	{
		msgbox("Am I dreaming…?\n" "Maybe the rumors were true…", MSGBOX_NPC)
	}
}
	
script DropperPoint_EventScript_Calvin
{
	lock
	msgbox("Five more minutes, mom…\n" "Zzzzz-huh?", MSGBOX_DEFAULT)
	setvar(VAR_TEMP_1, LOCALID_CALVIN)
	call(DropperPoint_CallScript_RivalGreetPlayer)
	clearflag(FLAG_HIDE_CALVIN_INTRO)
	msgbox("Whoa! {PLAYER}!? What are you\n" "doing in my…\p" "Hey, this isn't my room!\n" "Where are we? How'd we get here?\p"
			"Huh…you don't know me? I've seen\n" "you around on campus. Name's Calvin.\p" "But really, where are we, anyway?", MSGBOX_DEFAULT)
	setflag(FLAG_FAVORED_CALVIN)
	release
}
	
script DropperPoint_EventScript_CalvinAwake
{
	speakername(SpeakerName_Calvin)
	msgbox("Last thing I remember is, uh…\n" "I don't remember…\p" "I need some coffee…", MSGBOX_NPC)
}

script DropperPoint_TriggerScript_Bag
{
	lockall
	if (!flag(FLAG_FAVORED_CALVIN)) // Introduce Calvin if you didn't talk to him before checking the bag
	{
		setvar(VAR_LAST_TALKED, LOCALID_ASLEEP_CALVIN)
		call(DropperPoint_EventScript_WakeRival)
		addobject(LOCALID_CALVIN)
		clearflag(FLAG_HIDE_CALVIN_INTRO)
		applymovement(LOCALID_CALVIN, DropperPoint_Movement_WakeJump)
		waitmovement(0)
		applymovement(LOCALID_CALVIN, Common_Movement_FaceUp)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
		waitmovement(0)
		msgbox("Huh…? {PLAYER}, is that you?\n" "It's me, Calvin, from uni!\l" "Where the heck are we?", MSGBOX_DEFAULT)
		closemessage
	}
	if(!flag(FLAG_FAVORED_MORGAN)) // Introduce Morgan if you didn't talk to her before checking the bag
	{
		setvar(VAR_LAST_TALKED, LOCALID_ASLEEP_MORGAN)
		call(DropperPoint_EventScript_WakeRival)
		addobject(LOCALID_MORGAN)
		clearflag(FLAG_HIDE_MORGAN_INTRO)
		applymovement(LOCALID_MORGAN, DropperPoint_Movement_WakeJump)
		waitmovement(0)
		applymovement(LOCALID_MORGAN, Common_Movement_FaceRight)
		waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
		waitmovement(0)
		msgbox("Urgh…weird dream…\p" "Wait, are you {PLAYER}?\n" "I recognize you from college.\p" "What is this place? I don't remember\n"
				"how I got here…", MSGBOX_DEFAULT)
		closemessage
	}
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement(0)
	speakername(SpeakerName_Calvin)
	msgbox("Did'ja find something, {PLAYER}?\n" "Let me see.", MSGBOX_DEFAULT)
	closemessage
	setmetatile(12, 11, 0x36C, FALSE) // dumb thing to fix visual error
	special(DrawWholeMapView)
	applymovement(LOCALID_CALVIN, DropperPoint_Movement_CalvinJump1)
	playse(SE_BIKE_HOP)
	waitse
	waitmovement(0)
	applymovement(LOCALID_CALVIN, DropperPoint_Movement_CalvinJump2)
	playse(SE_BIKE_HOP)
	waitmovement(0)
	setmetatile(12, 11, 0x2E1, FALSE) // dumb thing to fix visual error
	special(DrawWholeMapView)
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_MorganGoToBag)
	waitmovement(0)
	applymovement(LOCALID_CALVIN, Common_Movement_FaceLeft)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement(0)
	lockall
	speakername(SpeakerName_Morgan)
	msgbox("So…not to point out the obvious,\n" "but nobody knows where we are or\l" "how we got here, right…?", MSGBOX_DEFAULT)
	speakername(SpeakerName_Calvin)
	msgbox("Sure doesn't seem like it.\n" "Last I remember, I took that dare to\l" "camp out in those creepy ruins.", MSGBOX_DEFAULT)
	speakername(SpeakerName_Morgan)
	msgbox("The ruins…! Do you think that maybe…\p" "No, it's too silly. Never mind.", MSGBOX_DEFAULT)
	speakername(SpeakerName_Calvin)
	msgbox("Some punk probably just moved us\n" "while we were sleeping as a prank.\p" "I'll look up a map.", MSGBOX_DEFAULT)
	closemessage
	lockall
	applymovement(LOCALID_CALVIN, Common_Movement_FaceUp)
	waitmovement(0)
	delay(60)
	speakername(SpeakerName_Calvin)
	msgbox("Dang, no signal.", MSGBOX_AUTOCLOSE)
	addobject(LOCALID_SLOSHMOSH)
	setobjectxy(LOCALID_SLOSHMOSH, 13, 14)
	applymovement(LOCALID_SLOSHMOSH, Common_Movement_WalkUp)
	playse(SE_BALLOON_BLUE)
	waitse
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_MorganRunToPlayer)
	waitmovement(0)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	applymovement(LOCALID_CALVIN, Common_Movement_FaceRight)
	waitmovement(0)
	speakername(SpeakerName_Morgan)
	msgbox("W-what was that noise?", MSGBOX_DEFAULT)
	speakername(SpeakerName_Calvin)
	msgbox("Relax. It was just a wave, or…", MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_SLOSHMOSH, Common_Movement_WalkUp2)
	playse(SE_BALLOON_BLUE)
	waitse
	playse(SE_BALLOON_BLUE)
	waitse
	waitmovement(0)
	applymovement(LOCALID_CALVIN, Common_Movement_FaceDown)
	applymovement(LOCALID_MORGAN, Common_Movement_FaceDown)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	lockall
	delay(60)
	delay(60)
	playbgm(MUS_HELP, TRUE)
	applymovement(LOCALID_SLOSHMOSH, DropperPoint_Movement_RushForward)
	playse(SE_BALLOON_BLUE)
	waitse
	playse(SE_BALLOON_BLUE)
	waitse
	applymovement(LOCALID_CALVIN, Common_Movement_ExclamationMark)
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_Jump)
	waitmovement(0)
	waitse
	playmoncry(SPECIES_SLOSHMOSH, CRY_MODE_NORMAL)
	msgbox("Spchwerrrrrr!", MSGBOX_DEFAULT)
	waitmoncry
	closemessage
	msgbox("MORGAN: Aaahh! What is that thing!?\n" "CALVIN: Punch it or something!", MSGBOX_DEFAULT)
	closemessage
	setvar(VAR_FONEI_COAST_STATE, 2)
	setrespawn(HEAL_LOCATION_CRAN_TOWN)
	setflag(FLAG_HIDE_FONEI_COAST_NPCS)
	releaseall
}

script DropperPoint_EventScript_Sloshmosh
{
	lock
	faceplayer
	waitse
	playmoncry(SPECIES_SLOSHMOSH, CRY_MODE_NORMAL)
	msgbox("Spchwerrrrrr!", MSGBOX_DEFAULT)
	waitmoncry
	release
}

script DropperPoint_EventScript_Bag
{
	setflag(FLAG_SYS_POKEMON_GET)
	fadescreen(FADE_TO_BLACK)
	removeobject(LOCALID_SLOSHMOSH)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement(0)
	special(ChooseStarter)
	waitstate
	bufferleadmonspeciesname(STR_VAR_1)
	msgbox("Give a nickname to your {STR_VAR_1}?", MSGBOX_YESNO)
	if(var(VAR_RESULT) == YES)
	{
		setvar(VAR_0x8004, 0)
		call(Common_EventScript_NameReceivedPartyMon)
	}
	closemessage
	lockall
	applymovement(LOCALID_MORGAN, Common_Movement_FaceLeft)
	applymovement(LOCALID_CALVIN, Common_Movement_FaceDown)
	applymovement(OBJ_EVENT_ID_PLAYER, DropperPoint_Movement_StepBack)
	waitmovement(0)
	lockall
	speakername(SpeakerName_Calvin)
	msgbox("How'd you make that monster appear?", MSGBOX_DEFAULT)
	speakername(SpeakerName_Morgan)
	msgbox("Huh? The bag…? Let me see…", MSGBOX_AUTOCLOSE)
	special(HealPlayerParty)
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_MorganGoToBagAgain)
	waitmovement(0)
	lockall
	delay(30)
	speakername(SpeakerName_Morgan)
	msgbox("What? These look like…Poké Balls…\n" "These monsters are…Pocket Monsters?", MSGBOX_DEFAULT)
	speakername(SpeakerName_Calvin)
	msgbox("Pokémon? Isn't that a video game?", MSGBOX_DEFAULT)
	speakername(SpeakerName_Morgan)
	msgbox("See for yourself! They're here!", MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_CALVIN, Common_Movement_FaceRight)
	waitmovement(0)
	lockall
	delay(30)
	speakername(SpeakerName_Calvin)
	msgbox("Huh…well whatever they are, if they\n" "can protect us from monsters,\l" "we'd better each take one.\l" "You can keep that one, {PLAYER}.")
	applymovement(LOCALID_CALVIN, Common_Movement_WalkInPlaceRight)
	speakername(SpeakerName_Morgan)
	msgbox("I don't recognize this Pokémon, but\n" "it's still kind of exciting…\l" "I wish I had this dream every night.", MSGBOX_DEFAULT)
	speakername(SpeakerName_Calvin)
	msgbox("We'd better get moving. If there's\n" "monsters about, we'd better hope we\l" "find a town before more show up." MSGBOX_DEFAULT)
	speakername(SpeakerName_Morgan)
	msgbox("Oh, yes. Hmm…maybe these Pokémon\n" "belong to someone who lives nearby?\p" "I'll bring the bag with me, just in case.", MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_CALVIN, DropperPoint_Movement_CalvinStartWalkingAway)
	playse (SE_BIKE_HOP)
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_MorganStartWalkingAway)
	removeobject(1)
	waitmovement(0)
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement(0)
	lockall
	speakername(SpeakerName_Morgan)
	msgbox("I hope you come too, {PLAYER}. I mean…\n" "Safety in numbers and all that, right?", MSGBOX_AUTOCLOSE)
	closemessage
	applymovement(LOCALID_MORGAN, DropperPoint_Movement_MorganFinishWalkingAway)
	waitmovement(0)
	removeobject(LOCALID_CALVIN)
	removeobject(LOCALID_MORGAN)
	clearflag(FLAG_FAVORED_MORGAN)
	clearflag(FLAG_FAVORED_CALVIN)
	releaseall
}

script DropperPoint_TriggerScript_Roadblock
{
	lock
	if (flag(FLAG_FAVORED_CALVIN))
	{
		speakername(SpeakerName_Calvin)
		msgbox("{PLAYER}! Where ya going? There's\n" "something here you should check out!", MSGBOX_AUTOCLOSE)
	}
	else
	{
		msgbox("What about those other people…?", MSGBOX_AUTOCLOSE)
	}
	applymovement(OBJ_EVENT_ID_PLAYER, DropperPoint_Movement_ComeBack)
	waitmovement(0)
	release
}

movement DropperPoint_Movement_WakeUp
{
	face_up
	delay_16
	face_down
	delay_8
	face_up
	delay_16
	face_down
	delay_8
	face_up
	delay_16
	face_left
}

movement DropperPoint_Movement_WakeJump
{
	set_visible
	jump_in_place_down
	delay_16 * 2
}

movement DropperPoint_Movement_CalvinJump1
{
	jump_2_up
	walk_up
}

movement DropperPoint_Movement_CalvinJump2
{
	jump_2_up
	face_right
}

movement DropperPoint_Movement_MorganGoToBag
{
	walk_right * 2
	delay_16
	walk_right * 2
}

movement DropperPoint_Movement_MorganRunToPlayer
{
	walk_fast_right * 4
	walk_fast_up
	face_down
}

movement DropperPoint_Movement_RushForward
{
	walk_fast_up * 2
}

movement DropperPoint_Movement_Jump
{
	jump_in_place_down
}

movement DropperPoint_Movement_StepBack
{
	walk_down
	face_up
}

movement DropperPoint_Movement_MorganGoToBagAgain
{
	walk_left
	face_up
}

movement DropperPoint_Movement_CalvinStartWalkingAway
{
	jump_2_down
	walk_down * 2
	walk_right * 5
	walk_down * 3
	walk_right * 4
}

movement DropperPoint_Movement_MorganStartWalkingAway
{
	walk_in_place_up
	delay_16
	walk_right
	walk_down
	walk_right * 2
	delay_8
	face_left
	delay_16
	face_right
	delay_16 * 2
	face_left
	delay_16
}

movement DropperPoint_Movement_MorganFinishWalkingAway
{
	lock_facing_direction
	walk_slow_right
	jump_left
	unlock_facing_direction
	face_right
	delay_16 * 2
	walk_fast_down * 4
	walk_fast_right * 5
}

movement DropperPoint_Movement_ComeBack
{
	walk_left * 4
}
	
